"          __
"  __  __ /\_\    ___ ___   _ __   ___
" /\ \/\ \\/\ \ /' __` __`\/\`'__\/'___\
" \ \ \_/ |\ \ \/\ \/\ \/\ \ \ \//\ \__/
"  \ \___/  \ \_\ \_\ \_\ \_\ \_\\ \____\
"   \/__/    \/_/\/_/\/_/\/_/\/_/ \/____/

" vimrcã§ãƒãƒ«ãƒãƒã‚¤ãƒˆæ–‡å­—ã‚’ä½¿ç”¨ã—ã¦ã„ã‚‹ãŸã‚è¨­å®š {{{
scriptencoding utf-8
" }}}

" dein.vim settings {{{
" install dir {{{
let s:dein_dir = expand('~/.cache/dein')
let s:dein_repo_dir = s:dein_dir . '/repos/github.com/Shougo/dein.vim'
" }}}

" dein installation check {{{
if &runtimepath !~# '/dein.vim'
  if !isdirectory(s:dein_repo_dir)
    execute '!git clone https://github.com/Shougo/dein.vim' s:dein_repo_dir
  endif
  execute 'set runtimepath^=' . s:dein_repo_dir
endif
" }}}

" begin settings {{{
if dein#load_state(s:dein_dir)
  call dein#begin(s:dein_dir)

  " .toml file
  let s:rc_dir = expand('~/.vim')
  if !isdirectory(s:rc_dir)
    call mkdir(s:rc_dir, 'p')
  endif
  let s:toml = s:rc_dir . '/dein.toml'

  " read toml and cache
  call dein#load_toml(s:toml, {'lazy': 0})

  " end settings
  call dein#end()
  call dein#save_state()
endif
" }}}

" plugin installation check {{{
if dein#check_install()
  call dein#install()
endif
" }}}

" plugin remove check {{{
let s:removed_plugins = dein#check_clean()
if len(s:removed_plugins) > 0
  call map(s:removed_plugins, "delete(v:val, 'rf')")
  call dein#recache_runtimepath()
endif
" }}}

" }}}

" general settings {{{

" ãƒŸãƒ¥ãƒ¼ãƒˆ {{{
set belloff=all
" }}}

" æ–‡å­—ã‚³ãƒ¼ãƒ‰ {{{
set encoding=utf-8
set fileencodings=utf-8,iso-2022-jp,euc-jp,sjis
set fileformats=unix,dos,mac
" }}}

" Leaderã‚­ãƒ¼ã‚’ã‚¹ãƒšãƒ¼ã‚¹ã«è¨­å®š {{{
let g:mapleader = "\<Space>"
" }}}

" ã‚·ãƒ³ã‚¿ãƒƒã‚¯ã‚¹ã‚’æœ‰åŠ¹ã«ã™ã‚‹ {{{
syntax enable
" }}}

" ã‚«ãƒ©ãƒ¼ã‚¹ã‚­ãƒ¼ãƒ ã‚’ä½¿ã† {{{
colorscheme iceberg
set bg=dark
" }}}

" ãƒ•ã‚¡ã‚¤ãƒ«å½¢å¼åˆ¥ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ã¨ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆã‚’æœ‰åŠ¹ã«ã™ã‚‹ {{{
filetype plugin indent on
" }}}

" ãƒãƒƒã‚¯ã‚¹ãƒšãƒ¼ã‚¹ã¨Ctrl+hã§å‰Šé™¤ã‚’æœ‰åŠ¹ã«ã™ã‚‹ {{{
set backspace=2
" }}}

" æ”¹è¡Œæ™‚è‡ªå‹•ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆ {{{
set smartindent
set autoindent
" }}}

" è‡ªå‹•ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆã®ç©ºç™½ã®æ•°
"set shiftwidth=2
" }}}

" è¡Œç•ªå·ã‚’è¡¨ç¤º {{{
"set number
" }}}

" ã‚¿ãƒ–ã§ã‚‚å¸¸ã«ç©ºç™½ã‚’æŒ¿å…¥ {{{
"set tabstop=4
"set expandtab
" }}}

" ã‚¤ãƒ³ã‚¯ãƒªãƒ¡ãƒ³ãƒˆã‚µãƒ¼ãƒã‚’æœ‰åŠ¹ã«ã™ã‚‹ {{{
set incsearch
" }}}

" æ¤œç´¢æ™‚å¤§æ–‡å­—å°æ–‡å­—ã‚’åŒºåˆ¥ã—ãªã„ {{{
set ignorecase
" }}}

" æ¤œç´¢æ™‚ã«å¤§æ–‡å­—ã‚’å…¥åŠ›ã—ãŸå ´åˆignorecaseãŒç„¡åŠ¹ã«ãªã‚‹ {{{
set smartcase
" }}}

" ãƒã‚¤ãƒ©ã‚¤ãƒˆã‚µãƒ¼ãƒã‚’æœ‰åŠ¹ã«ã™ã‚‹ {{{
set hlsearch
" }}}

" undoã§ãã‚‹æœ€å¤§æ•° {{{
set undolevels=1000
" }}}

" ã‚¯ãƒªãƒƒãƒ—ãƒœãƒ¼ãƒ‰ã‚’å…±æœ‰ {{{
if has("mac")
  set clipboard+=unnamed
else
  set clipboard^=unnamedplus
endif
" }}}

" ã‚«ãƒ¼ã‚½ãƒ«ãŒå¸¸ã«ä¸­å¤®ã«æ¥ã‚‹ã‚ˆã†ã«ã™ã‚‹ {{{
set scrolloff=100
" }}}

" ãƒã‚¯ãƒ­ã§åŠ¹æœç™ºæ® {{{
set lazyredraw
set ttyfast
" }}}

" ä¸€è¡ŒãŒé•·ã„ãƒ•ã‚¡ã‚¤ãƒ«ã‚’syntaxã‚’åˆ¶å¾¡ã™ã‚‹ã“ã¨ã§è»½ãã™ã‚‹ {{{
set synmaxcol=256
" }}}

" ã‚«ãƒ¼ã‚½ãƒ«ãƒ©ã‚¤ãƒ³ã‚’è¡¨ç¤ºã™ã‚‹ {{{
set cursorline
" æç”»è² æ‹…è»½æ¸›ã®ãŸã‚ã€è¡Œç•ªå·ã®ã¿ãƒã‚¤ãƒ©ã‚¤ãƒˆ
if !has('nvim')
  set cursorlineopt=number
endif
" }}}

" netrwãƒ„ãƒªãƒ¼è¡¨ç¤ºã‚’æœ‰åŠ¹ã«ã™ã‚‹ {{{
let g:netrw_liststyle=3
let g:netrw_banner=0
let g:netrw_sizestyle='H'
let g:netrw_timefmt='%Y/%m/%d(%a) %H:%M:%S'
" }}}

" æ‹¡å¼µå­ã”ã¨ã®ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆè¨­å®š {{{
augroup fileTypeIndent
  au!
  au FileType go setlocal tabstop=4 shiftwidth=4
  au FileType vim setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType php setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType html setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType javascript setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType typescript setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType vue  setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType yaml setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType json setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType sh setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType fish setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
augroup END
" }}}

" wildmenuã‚’æœ‰åŠ¹ã«ã™ã‚‹ {{{
set wildmenu
" }}}

" grepã—ãŸçµæœã‚’quickfixã«è¡¨ç¤ºã™ã‚‹ {{{
augroup grepwindow
  au!
  au QuickFixCmdPost *grep* cwindow
augroup END
" }}}

" ã‚«ãƒ¼ã‚½ãƒ«ãƒ©ã‚¤ãƒ³ã®ä½ç½®ã‚’ä¿å­˜ã™ã‚‹ {{{
augroup cursorlineRestore
  au!
  au BufReadPost *
        \ if line("'\"") > 0 && line("'\"") <= line("$") |
        \   exe "normal! g'\"" |
        \ endif
augroup END
" }}}

" undoã®ä¿å­˜å…ˆ {{{
if has('persistent_undo')
  let undo_path = expand('~/.vim/undo')
  " ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªãŒå­˜åœ¨ã—ãªã„å ´åˆã¯ä½œæˆ
  if !isdirectory(undo_path)
    call mkdir(undo_path, 'p')
  endif
  exe 'set undodir=' .. undo_path
  set undofile
endif
" }}}

" çŸ©å½¢é¸æŠæ™‚ã«æ–‡å­—ã®ç„¡ã„ã¨ã“ã‚ã¾ã§é¸æŠç¯„å›²ã‚’åºƒã’ã‚‹ {{{
set virtualedit=block
" }}}

" ãƒ˜ãƒ«ãƒ—ã®è¨€èªã‚’æ—¥æœ¬èªå„ªå…ˆã«ã™ã‚‹ {{{
set helplang=ja
" }}}

" {{{ ä»–ã®ãƒãƒƒãƒ•ã‚¡ã«ç§»å‹•ã™ã‚‹æ™‚ã«è‡ªå‹•ä¿å­˜
set autowrite
" }}}

" ãƒ•ã‚¡ã‚¤ãƒ«ä¿å­˜æ™‚ã«æ•´å½¢ã™ã‚‹ {{{
let s:format_targets = {
      \ 'javascript': '--use-tabs=false --single-quote=true %',
      \ 'html': '--use-tabs=false --single-quote=true %',
      \ 'css': '',
      \ 'json': '--tab',
      \ 'vue':  '--use-tabs=false --single-quote=true %',
      \ 'vim': '',
      \ 'java': '',
      \ }

" format function
function! Format() abort
  " if format target is not exsist, nothing to do
  if !has_key(s:format_targets, &ft)
    return
  endif

  let args = s:format_targets[&ft]
  let pos = getcurpos()

  " use js-beautify to format js, html, css
  if &ft is# 'javascript' || &ft is# 'html' || &ft is# 'vue'
    if executable('prettier')
      exe '%!prettier ' .. args
    else
      call s:echo_err("prettier doesn't installed, please refference the https://github.com/beautify-web/js-beautify")
    endif
  elseif &ft is# 'css'
    " TODO
  elseif &ft is# 'json'
    if executable('jq')
      exe "%!jq " .. args
    else
      call s:echo_err("jq doesn't installed, please refference the https://stedolan.github.io/jq/")
    endif
  else
    execute 'normal 1G=G'
  endif

  call setpos('.', pos)
endfunction

nnoremap <C-f> :call Format()<CR>
" }}}

" listã®è¨­å®š {{{
set list
set listchars=tab:Â»-,trail:-,extends:Â»,precedes:Â«,nbsp:%
" }}}

" {{{ è¡Œæœ«ã®ãƒ›ãƒ¯ã‚¤ãƒˆã‚¹ãƒšãƒ¼ã‚¹å‰Šé™¤
let s:spaces_target_ft = {
      \ 'markdown': 0,
      \ 'go': 1,
      \ 'javascript': 1,
      \ 'graphql': 1,
      \ 'terminal': 1,
      \ }
augroup HighlightTrailingSpaces
  au!
  au BufWritePre * if get(s:spaces_target_ft, &ft, 0) | :silent keeppatterns %s/\s\+$//ge | endif
  au VimEnter,WinEnter,ColorScheme * highlight TrailingSpaces term=underline guibg=Red ctermbg=Red
  au VimEnter,WinEnter * if get(s:spaces_target_ft, &ft, 0) | match TrailingSpaces /\s\+$/ | endif
augroup END
" }}}

" swapãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½œæˆã—ãªã„
set noswapfile

" {{{ ã‚»ãƒƒã‚·ãƒ§ãƒ³ã§ä¿å­˜ã™ã‚‹å¯¾è±¡ã‚’è¨­å®šã™ã‚‹
if !has('nvim')
  set sessionoptions=blank,buffers,curdir,folds,help,tabpages,winsize,terminal
endif
" }}}

" ã‚¦ã‚£ãƒ³ãƒ‰ã‚¦ã‚µã‚¤ã‚ºè‡ªå‹•èª¿æ•´ã‚’ç„¡åŠ¹åŒ–
"set noequalalways

" ã‚¿ãƒ–ã‚’å¸¸ã«è¡¨ç¤º
set showtabline=2

" æ•°å€¤ã®åŠ æ¸›ç®—ã‚’è€ƒæ…®
if has('patch-8.2.0860')
  setglobal nrformats+=unsigned
endif

" {{{ å¤–éƒ¨ã§ãƒ•ã‚¡ã‚¤ãƒ«ã®å¤‰æ›´ãŒã‚ã£ãŸå ´åˆã€è‡ªå‹•çš„ã«èª­ã¿ç›´ã™
set autoread
augroup vimrc-misc
  au!
  au WinEnter,FocusGained * checktime
augroup END
" }}}

" key mappings {{{
imap <C-h> <BS>

nmap gr <Plug>(Translate)
vmap gt <Plug>(VTranslate)

" fern
nnoremap <silent> <Leader>f :Fern . -drawer<CR>
function! s:fern_init() abort
  nnoremap <buffer> <silent> q :q<CR>
  map <buffer> <silent> <C-v> <Plug>(fern-action-open:split)
endfunction

augroup fern-setteings
  au!
  au FileType fern call s:fern_init()
augroup END

let g:fern#disable_viewer_hide_cursor = 1
"let g:fern#drawer_keep = 1

" *ã§ã‚«ãƒ¼ã‚½ãƒ«ã‚’ç§»å‹•ã—ãªã„ã‚ˆã†ã«ã™ã‚‹
noremap * *N

" ãƒ•ã‚¡ã‚¤ãƒ«ä¿å­˜ã¨çµ‚äº† {{{
nnoremap <Leader>w :w<CR>
" }}}

" æ¤œç´¢
nnoremap <C-G><C-G> :Grep <C-R><C-W><CR>

" ç½®æ›
nnoremap <Leader>re :%s;\<<C-R><C-W>\>;g<Left><Left>;

" ãƒã‚¤ãƒ©ã‚¤ãƒˆã‚’å‰Šé™¤ã™ã‚‹
nnoremap <silent> <Esc><Esc> :nohlsearch<CR>

" vimrcã‚’é–‹ã
nnoremap <Leader>. :new ~/.vimrc<CR>
nnoremap <Leader>s :source ~/.vimrc<CR>

" ãƒ†ã‚­ã‚¹ãƒˆã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚­ãƒ¼ãƒãƒƒãƒ”ãƒ³ã‚° {{{
onoremap 8 i(
onoremap 2 i"
onoremap 7 i'
onoremap @ i`
onoremap [ i[
onoremap { i{

onoremap a8 a(
onoremap a2 a"
onoremap a7 a'
onoremap a@ a`
onoremap a[ a[
onoremap a{ a{

" visual
nnoremap v8 vi(
nnoremap v2 vi"
nnoremap v7 vi'
nnoremap v@ vi`
nnoremap v[ vi[
nnoremap v{ vi{

nnoremap va8 va(
nnoremap va2 va"
nnoremap va7 va'
nnoremap va@ va`
nnoremap va[ va[
nnoremap va{ va{
" }}}

" è¡Œå…ˆé ­ã¨è¡Œæœ«
noremap H ^
noremap L g_

" ã‚¿ãƒ–åˆ‡ã‚Šæ›¿ãˆ
nnoremap <C-l> gt
nnoremap <C-h> gT

" numberã¨relativenumberã®åˆ‡ã‚Šæ›¿ãˆ
nnoremap <silent> <Leader>n :set relativenumber!<CR>

" ã‚¿ãƒ¼ãƒŸãƒŠãƒ«é–¢é€£ {{{
" ã‚¿ãƒ¼ãƒŸãƒŠãƒ«ãƒãƒ¼ãƒãƒ«ãƒ¢ãƒ¼ãƒ‰
tnoremap <C-w>n <C-w>N

" ã‚¿ãƒ¼ãƒŸãƒŠãƒ«ã§ã‚¦ã‚£ãƒ³ãƒ‰ã‚¦ç§»å‹•
tnoremap <silent> <C-j> <C-w>:tabnext<CR>
tnoremap <silent> <C-k> <C-w>:tabprevious<CR>

" <c-g>ã‚’<c-w>ä»£ã‚ã‚Šã«ã™ã‚‹
tnoremap <C-g> <C-w>.
" }}}

" review
augroup review
  au!
  au FileType review vnoremap <silent> <buffer> <Leader>hi c@<cursor>{<c-r>"}<esc>
augroup END

" æ”¹è¡Œ
nnoremap <C-j> o<ESC>
nnoremap <C-k> O<ESC>
nnoremap o A<CR>

" æŒ¿å…¥ãƒ¢ãƒ¼ãƒ‰ã§ã®ã‚«ãƒ¼ã‚½ãƒ«ç§»å‹•
inoremap <silent> <C-f> <Right>
inoremap <silent> <C-b> <Left>
inoremap <silent> <C-e> <Esc>A
inoremap <silent> <C-a> <Esc>I

" å›²ã†
nnoremap <silent> gw[ cw``<Esc>P
vnoremap <silent> gw[ c``<Esc>P

" gina
nnoremap <silent> gs :Gina status -s<CR>
nnoremap <silent> gl :Gina log<CR>
nnoremap <silent> gm :Gina blame<CR>
nnoremap <silent> gb :Gina branch<CR>
call gina#custom#mapping#nmap(
      \ 'status', 'gp',
      \ ':<C-u>Gina push<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'cm',
      \ ':<C-u>Gina commit<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'ca',
      \ ':<C-u>Gina commit --amend<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'dp',
      \ '<Plug>(gina-patch-oneside-tab)',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'ga',
      \ '--',
      \ {'silent': 1},
      \)
call gina#custom#mapping#vmap(
      \ 'status', 'ga',
      \ '--',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'log', 'dd',
      \ '<Plug>(gina-changes-of)',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'branch', 'n',
      \ '<Plug>(gina-branch-new)',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ '/.*', 'q',
      \ ':<C-u>bw!<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)

" ãƒ˜ãƒ«ãƒ—
augroup help-mapping
  au!
  au FileType help nnoremap <buffer> <silent>q :bw!<CR>
augroup END

" }}}

" ã‚³ãƒãƒ³ãƒ‰ãƒ©ã‚¤ãƒ³ã§å˜èªç§»å‹• {{{
cnoremap <c-b> <Left>
cnoremap <c-f> <Right>
cnoremap <c-a> <Home>
" }}}

" Better <C-n>/<C-p> in Command {{{
cnoremap <C-p> <Up>
cnoremap <C-n> <Down>
cnoremap <Up> <C-p>
cnoremap <Down> <C-n>
" }}}

" {{{ quick run
nnoremap <Leader>q :QuickRun<CR>
" }}}

" {{{ code2img
map gi <Plug>(Code2img)
xmap gi <Plug>(Code2img)
let g:code2img_line_number = 1
" }}}

" vim-go settings {{{
" ãƒ•ã‚¡ã‚¤ãƒ«ä¿å­˜æ™‚go importã‚’å®Ÿè¡Œã™ã‚‹
let g:go_fmt_command = 'goimports'

" goplsã‚’æœ‰åŠ¹åŒ–
let g:go_gopls_enabled = 0

" ãƒ•ã‚¡ã‚¤ãƒ«ä¿å­˜æ™‚ã€linterã‚’å®Ÿè¡Œã™ã‚‹
"let g:go_metalinter_autosave = 1

" linterå®Ÿè¡Œæ™‚ã€go vetã®ã¿ã‚’å®Ÿè¡Œã™ã‚‹
"let g:go_metalinter_autosave_enabled = ['vet']

" golangci-lintã‚’ä½¿ã†
"let g:go_metalinter_command = "golangci-lint"

" vim-lspã‚’ä½¿ç”¨ã™ã‚‹ã®ã§ã€vim-goã®`Ctrl+]`ã‚’ç„¡åŠ¹ã«ã™ã‚‹
let g:go_def_mapping_enabled = 0

" GoDocã§ãƒãƒƒãƒ—ã‚¢ãƒƒãƒ—ã‚¦ã‚£ãƒ³ãƒ‰ã‚¦ã‚’ä½¿ç”¨ã™ã‚‹
"let g:go_doc_popup_window = 1

" GoRunã‚„GoTestæ™‚ã®ç”»é¢åˆ†å‰²æ–¹æ³•å¤‰æ›´
let g:go_term_mode = 'split'

" ãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆä½œæˆã‚’ç„¡åŠ¹åŒ–
let g:go_template_autocreate = 0

" ã™ã§ã«é–‹ã„ã¦ã„ã‚‹ãƒãƒƒãƒ•ã‚¡ã«å®šç¾©ã‚¸ãƒ£ãƒ³ãƒ—ã™ã‚‹
let g:go_def_reuse_buffer = 1

let g:go_fold_enable = ['block', 'import', 'varconst', 'package_comment']

" key mapping
augroup goMapping
  au!
  au FileType go set foldmethod=syntax
augroup END
" }}}

" fzf settings {{{
" ãƒ•ã‚¡ã‚¤ãƒ«ä¸€è¦§ã‚’å‡ºã™ã¨ãã«ãƒ—ãƒ¬ãƒ“ãƒ¥ãƒ¼è¡¨ç¤º
command! -bang -nargs=? -complete=dir Files
      \ call fzf#vim#files(<q-args>, fzf#vim#with_preview(), <bang>0)

nnoremap <C-P> :Files<CR>
" }}}

" lsp settings {{{
let g:lsp_signs_error = {'text': 'ï½³ï¾'}
let g:lsp_signs_warning = {'text': 'ğŸŒ'}
let g:lsp_diagnostics_float_cursor = 1
"let g:lsp_log_verbose = 1
"let g:lsp_log_file = expand('~/vim-lsp.log')

nmap <Leader>ho <plug>(lsp-hover)
nnoremap <silent> <C-]> :LspDefinition<CR>
nnoremap <silent> gn :LspNextError<CR>
nnoremap <silent> gp :LspPreviousError<CR>

function! s:on_lsp_buffer_enabled() abort
  setlocal completeopt=menu
  setlocal omnifunc=lsp#complete
  let g:lsp_settings_root_markers = ['go.mod'] + g:lsp_settings_root_markers
endfunction

let g:lsp_settings = {
      \ 'gopls': {
      \  'workspace_config': {
      \    'usePlaceholders': v:true,
      \    'analyses': {
      \      'fillstruct': v:true,
      \    },
      \  },
      \  'initialization_options': {
      \    'usePlaceholders': v:true,
      \    'analyses': {
      \      'fillstruct': v:true,
      \    },
      \  },
      \ },
      \}

augroup lsp_install
  au!
  au User lsp_buffer_enabled call s:on_lsp_buffer_enabled()
augroup END

" }}}

" vim-markdown settings {{{
" mdã‚’é–‹ãã¨ãã®æŠ˜ã‚ŠãŸãŸã¿ã‚’ç„¡åŠ¹ã«ã™ã‚‹
let g:vim_markdown_folding_disabled = 1
" }}}

" profiling {{{
" vim +'call ProfileCursorMove()' <ã‚«ãƒ¼ã‚½ãƒ«ã‚’å‹•ã‹ã™ã®ãŒé‡ã„ãƒ•ã‚¡ã‚¤ãƒ«>
function! ProfileCursorMove() abort
  let profile_file = expand('./vim-profile.log')
  if filereadable(profile_file)
    call delete(profile_file)
  endif

  normal! ggzR

  execute 'profile start ' . profile_file
  profile func *
  profile file *

  augroup ProfileCursorMove
    au!
    au CursorHold <buffer> profile pause | q
  augroup END

  for i in range(1000)
    call feedkeys('j')
  endfor
endfunction
" }}}

" emmet {{{
let g:user_emmet_install_global = 0
let g:user_emmet_settings = {
      \   'variables': {
      \     'lang' : 'ja'
      \   }
      \ }
let g:user_emmet_leader_key = '<C-g>'

augroup emmet
  au!
  au FileType vue,html,css EmmetInstall
  au FileType vue,html,css imap <buffer> <C-f> <plug>(emmet-expand-abbr)
augroup END
" }}}

" vim-vue {{{
" https://github.com/posva/vim-vue#my-syntax-highlighting-stops-working-randomly
" vueãƒ•ã‚¡ã‚¤ãƒ«ã®ã‚·ãƒ³ã‚¿ãƒƒã‚¯ã‚¹ãŒåŠ¹ã‹ãªããªã‚‹å•é¡Œå¯¾å¿œ
augroup VueSyntax
  au!
  au FileType vue syntax sync fromstart
augroup END
" }}}

" sonictemplate.vim {{{
let g:sonictemplate_author = 'skanehira'
let g:sonictemplate_license = 'MIT'
let g:sonictemplate_vim_template_dir = expand('~/.vim/sonictemplate')
imap <silent> <C-l> <plug>(sonictemplate-postfix)
" }}}

" vimhelpgenarator {{{
let g:vimhelpgenerator_version = ''
let g:vimhelpgenerator_author = 'Author: skanehira <sho19921005@gmail.com>'
let g:vimhelpgenerator_uri = 'https://github.com/skanehira/'
let g:vimhelpgenerator_defaultlanguage = 'en'
" }}}

" {{{ MemoList
let g:memolist_fzf = 1
" }}}

" {{{ lexima
let g:lexima_enable_basic_rules = 1
" }}}

" {{{ docker.vim
"let g:docker_terminal_open = 'vert'
let g:docker_use_tmux = 1

"let s:docker_auth_file = expand('~/.docker/docker.vim.json')
"let g:docker_registry_auth = json_decode(join(readfile(s:docker_auth_file), "\n"))
" }}}

" {{{ preview.vim
let g:previm_open_cmd = 'open'
let g:previm_plantuml_imageprefix = 'http://localhost:8888/png/'
" }}}

" {{{ gofmtmd
let g:gofmtmd_auto_fmt = 0
" }}}

" {{{ preview-markdown.vim
let g:preview_markdown_vertical = 1
let g:preview_markdown_auto_update = 1
let g:preview_markdown_parser = 'mdr'
" }}}

" {{{ vista
let g:vista_default_executive = 'vim_lsp'
" }}}

"{{{ gist-vim
let g:gist_post_private = 1
let g:gist_list_vsplit = 1
"}}}

" {{{ tweetvim
nnoremap <silent> <Leader>th :<C-u>TweetVimHomeTimeline<CR>
nnoremap <silent> <Leader>ts :<C-u>TweetVimSay<CR>
nnoremap <silent> <Leader>tm :<C-u>TweetVimMentions<CR>
let g:tweetvim_display_time = 1
let g:tweetvim_open_buffer_cmd = 'tabnew'
" }}}

" {{{ è‡ªä½œé–¢æ•°

" {{{ ãƒ¬ã‚¸ã‚¹ã‚¿ã‚¯ãƒªã‚¢
function! s:clear_register() abort
  let rs = split('abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ0123456789/-"', '\zs')
  for r in rs
    call setreg(r, [])
  endfor
endfunction
" }}}

command! ClearRegister call s:clear_register()
" {{{ w3m
function! s:gg(package) abort
  execute('term ++close ++shell w3m pkg.go.dev/' . a:package)
endfunction

command! -nargs=1 GG call s:gg(<f-args>)

function! s:www(word) abort
  execute('term ++close ++shell w3m google.com/search\?q="' . a:word . '"')
endfunction

command! -nargs=1 WWW call s:www(<f-args>)
" }}}

" {{{ ãƒªãƒã‚¸ãƒˆãƒªã«ç§»å‹•
function! s:cd_repo(shell, repo) abort
  exe 'lcd' trim(system('ghq root')) .. '/' .. a:repo
  pwd
endfunction

function! s:repo(cb) abort
  if executable('ghq') && exists('*fzf#run()') && executable('fzf')
    call fzf#run({
          \ 'source': systemlist('ghq list'),
          \ 'sink': a:cb,
          \ 'down': '40%'},
          \ )
  else
    echo "doesn't installed ghq or fzf.vim(require fzf)"
  endif
endfunction

command! Repo call s:repo(function('s:cd_repo', [&shell]))
" }}}

" æ–°ã—ã„ã‚¿ãƒ–ã‚’é–‹ã {{{
function! s:open_tabs(shell, repo) abort
  exe printf('tabnew | lcd %s/%s', trim(system('ghq root')), a:repo)
endfunction

" fzf.vimã®callbackã§ã¯&shellãŒshã«ãªã£ã¦ã—ã¾ã†ã®ã§ã€ç¾åœ¨å®Ÿè¡Œã—ã¦ã„ã‚‹shellã‚’æ¸¡ã™
command! Repot call s:repo(function('s:open_tabs', [&shell]))

" }}}

" {{{ ã‚¨ãƒ©ãƒ¼ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸å‡ºåŠ›
function! s:echo_err(message) abort
  echohl ErrorMsg
  redraw
  echo a:message
  echohl None
endfunction
" }}}

" {{{ gitã‚³ãƒãƒ³ãƒ‰é–¢é€£
" options:
" {
"	cmd: 'diff'
"	args: [
"		'HEAD^',
"	],
"	auto_close: 0,	" 1: true, 0: false
"	window_way: 'bo',	" tab, bo, vert, term, etc...
"	mode: 'term', " term, popup, fzf
"	focus: 1008, " window id
"	source: [], " fzf source
"	cb: functionm "fzf callback
" }
function! s:git_exec(opt) abort
  let current_winid = win_getid()
  if a:opt.mode is# 'term'
    let cmd = 'git ' .. a:opt.cmd
    if has_key(a:opt, 'args') && !empty(a:opt.args)
      let cmd = cmd .. ' ' .. join(a:opt.args, ' ')
    endif
    if has_key(a:opt, 'auto_close') && a:opt.auto_close is# 1
      let cmd = a:opt.window_way .. ' term ++rows=10 ++close ' .. cmd
    else
      let cmd = a:opt.window_way .. ' term ++rows=10 ' .. cmd
    endif
    exec cmd
    nnoremap <buffer> <silent> q :bw!<CR>
  elseif a:opt.mode is# 'pop'
    " TODO use popup window
  elseif a:opt.mode is# 'fzf'
    if exists('*fzf#run()') && executable('fzf')
      call fzf#run({
            \ 'source': a:opt.source,
            \ 'sink': a:opt.cb,
            \ 'down': '40%'},
            \ )
    endif
  else
    call s:echo_err("doesn't installed fzf.vim(require fzf)")
  endif

  call win_gotoid(current_winid)
endfunction

function! s:git_log() abort
  " use fish command
  call system('tmux new-window git log')
endfunction

function! s:git_push(...) abort
  let opt = {
        \ 'cmd': 'push',
        \ 'args': a:000,
        \ 'auto_close': 1,
        \ 'window_way': 'top',
        \ 'mode': 'term',
        \ }

  call s:git_exec(opt)
endfunction

function! s:git_pull(...) abort
  let opt = {
        \ 'cmd': 'pull',
        \ 'args': a:000,
        \ 'auto_close': 1,
        \ 'window_way': 'top',
        \ 'mode': 'term',
        \ }

  call s:git_exec(opt)
endfunction

function! s:git_checkout_cb(repo) abort
  call system('git checkout' .. a:repo)
endfunction

function! s:git_checkout(...) abort
  let source = filter(systemlist('git branch'), 'v:val[0] isnot# "*"')
  if empty(source)
    call s:echo_err("no other branch")
    return
  endif

  let opt = {
        \ 'source': source,
        \ 'mode': 'fzf',
        \ 'cb': function('s:git_checkout_cb'),
        \ }

  call s:git_exec(opt)
endfunction

command! -nargs=* Gitpush call s:git_push(<f-args>)
command! -nargs=* Gitpull call s:git_pull()
command! -nargs=* GitCheckout call s:git_checkout(<f-args>)
nnoremap <silent> gc :GitCheckout<CR>

" }}}

" {{{ ãƒãƒƒãƒ•ã‚¡é–¢é€£
function! s:buflist() abort
  return filter(range(1, bufnr('$')), 'buflisted(v:val) && getbufvar(v:val, "&filetype") != "qf"')
endfunction

function! s:format_buffer(b)
  let name = bufname(a:b)
  let name = empty(name) ? '[No Name]' : fnamemodify(name, ":p:~:.")
  let flag = a:b == bufnr('')  ? '%' : a:b == bufnr('#') ? '#' : ' '
  let modified = getbufvar(a:b, '&modified') ? ' [+]' : ''
  let readonly = getbufvar(a:b, '&modifiable') ? '' : ' [RO]'
  let extra = join(filter([modified, readonly], '!empty(v:val)'), '')
  return printf("[%s] %s\t%s\t%s", a:b, flag, name, extra)
endfunction

function! s:delbuf(buf) abort
  exe 'bw!' a:buf[match(a:buf, '[')+1:match(a:buf, ']')-1]
endfunction

function! s:delbuflist() abort
  call fzf#run({
        \ 'source': map(copy(s:buflist()), 's:format_buffer(v:val)'),
        \ 'sink': function('s:delbuf'),
        \ 'options': "-m",
        \ 'down': '40%'},
        \ )
endfunction

command! DelBuf call s:delbuflist()
" }}}

" {{{ ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªè‡ªå‹•ç”Ÿæˆ
augroup vimrc-auto-mkdir
  au!
  au BufWritePre * call s:auto_mkdir(expand('<afile>:p:h'), v:cmdbang)
  function! s:auto_mkdir(dir, force)
    if !isdirectory(a:dir) && (a:force ||
          \    input(printf('"%s" does not exist. Create? [y/N]', a:dir)) =~? '^y\%[es]$')
      call mkdir(iconv(a:dir, &encoding, &termencoding), 'p')
    endif
  endfunction
augroup END
" }}}

" {{{ ã‚¿ãƒ¼ãƒŸãƒŠãƒ«ãƒãƒƒãƒ•ã‚¡
" eg. vnew term://bsah ã§ã‚¿ãƒ¼ãƒŸãƒŠãƒ«ã‚’å®Ÿè¡Œ
function! s:termopen() abort
  let name = split(bufname(), '\/\/')
  if len(name) < 2
    return
  endif
  call execute(printf('term ++curwin ++close ++shell %s', name[1]))
endfunction

augroup terminal
  au!
  au BufReadCmd term://* call s:termopen()
augroup END
" }}}

" {{{ ç”»åƒä»˜ããƒ„ã‚¤ãƒ¼ãƒˆ
function! s:on_err_vim(ch, msg) abort
  echohl ErrorMsg
  echom '[code2img]' a:msg
  echohl None
endfunction

function! s:exit_twty_cb(tmp_img, tmp_tweet, ch, msg) abort
  call delete(a:tmp_img)
  call delete(a:tmp_tweet)
  echom 'tweet success'
endfunction

function! s:exit_code2imge_cb(tmp_img, tweet, tmp_code, ch, msg) abort
  call delete(a:tmp_code)

  let tmp_tweet = tempname()
  call writefile([a:tweet], tmp_tweet)

  call job_start(['twty', '-m', a:tmp_img, '-ff', '-'], {
        \ 'in_io': 'file',
        \ 'in_name': tmp_tweet,
        \ 'err_cb': function('s:on_err_vim'),
        \ 'exit_cb': function('s:exit_twty_cb', [a:tmp_img, tmp_tweet])
        \ })
endfunction

function! TweetWithImg(first, last, ...) abort
  let lines = getline(a:first, a:last)
  let tmp_code = printf("%s.%s", tempname(), &ft)
  call writefile(lines, tmp_code)

  let tmp_img = tempname()

  let cmd = ['code2img', '-l', '-t', 'solarized-dark', '-ext', &ft, '-o', tmp_img]

  call job_start(cmd, {
        \ 'in_io': 'file',
        \ 'in_name': tmp_code,
        \ 'err_cb': function('s:on_err_vim'),
        \ 'exit_cb': function('s:exit_code2imge_cb', [tmp_img, a:1, tmp_code])
        \ })
endfunction

command! -nargs=? -range TweetWithImg call TweetWithImg(<line1>, <line2>, <q-args>)
" }}}

" {{{ gina preview
function! s:clean_up() abort
  execute "tabclose!"
  let t:gina_preview = 0
  call gina#core#emitter#unsubscribe("command:called", function("s:boot"))
  augroup status-cursor
    au!
  augroup END
endfunction

function! s:boot(...) abort
  nnoremap <silent> <buffer> q :call <SID>clean_up()<CR>

  let l = substitute(getline("."), "\<Esc>[^m]\\+m", "", "g")
  if empty(l)
    return
  endif

  let type = l[0:2]
  let file = l[3:]

  if type[0] == "M"
    let cmd = ["git", "diff", "--cached"]
  elseif type[1] == "M"
    let cmd = ["git", "diff"]
  elseif type[0] == "D"
    let cmd = ["git", "diff", "--cached", "--"]
  elseif type[1] == "D"
    let cmd = ["git", "diff", "--"]
  elseif type[1] == "A"
    let cmd = ["git", "diff"]
  elseif file[-1:] == "/"
    let cmd = ["tree"]
  else
    let cmd = ["cat"]
  endif

  let cmd += [file]

  call win_gotoid(t:winid_preview)
  %d_
  call execute("0r!" . join(cmd, " "))
  normal! gg

  call win_gotoid(t:winid_gina)
endfunction

function! s:open_git_status_preview() abort
  if get(t:, "gina_preview", 0)
    return
  endif
  let t:gina_preview = 1
  tabnew
  Gina status -s

  let t:winid_gina = win_getid()

  belowright vnew
  set ft=diff
  set buftype=nofile
  let t:winid_preview = win_getid()

  call win_gotoid(t:winid_gina)
  execute "vertical resize" &columns / 3
  call gina#core#emitter#subscribe("command:called", function("s:boot"))

  augroup status-cursor
    au!
    autocmd CursorMoved <buffer> call s:boot()
  augroup END
endfunction

nnoremap <silent> gp :call <SID>open_git_status_preview()<CR>
" }}}

" {{{ gh.vim
let gh_token_file = expand('~/.gh-vim')
if filereadable(gh_token_file)
  let g:gh_token = trim(readfile(gh_token_file)[0])
  let g:gh_enable_delete_repository = 0
endif

function! s:gh_map_apply() abort
  call gh#map#add('gh-buffer-issue-list', 'map', 'e', '<Plug>(gh_issue_edit)')
  call gh#map#add('gh-buffer-issue-list', 'map', 'gm', '<Plug>(gh_issue_open_comment)')
  call gh#map#add('gh-buffer-issue-comment-list', 'map', 'n', '<Plug>(gh_issue_comment_new)')
  call gh#map#add('gh-buffer-issue-edit', 'map', 'gm', '<Plug>(gh_issue_comment_open_on_issue)')
endfunction

" key mapping
augroup gh-maps
  au!
  au VimEnter * call <SID>gh_map_apply()
augroup END
" }}}

" }}}

function! s:grep(word) abort
  let cmd = printf('ag -S %s', a:word)
  cexpr system(cmd) | cw
endfunction

command! -nargs=1 Grep call <SID>grep(<q-args>)

function! s:create_zenn_article(article_name) abort
  let date = strftime("%Y-%m-%d")
  let slug = date . "-" . a:article_name
  call system("npx zenn new:article --emoji ğŸ¦ --slug " . slug )
  let article_path = "articles/" . slug . ".md"
  exe "edit " . article_path
endfunction

command! -nargs=1 ZennCreateArticle call <SID>create_zenn_article(<f-args>)

" vim: foldmethod=marker
