"          __
"  __  __ /\_\    ___ ___   _ __   ___
" /\ \/\ \\/\ \ /' __` __`\/\`'__\/'___\
" \ \ \_/ |\ \ \/\ \/\ \/\ \ \ \//\ \__/
"  \ \___/  \ \_\ \_\ \_\ \_\ \_\\ \____\
"   \/__/    \/_/\/_/\/_/\/_/\/_/ \/____/

let g:mapleader = "\<Space>" " Leaderã‚­ãƒ¼ã‚’ã‚¹ãƒšãƒ¼ã‚¹ã«è¨­å®š

" ãƒ—ãƒ©ã‚°ã‚¤ãƒ³è¨­å®š {{{
" {{{ dein.vim settings
if has('nvim')
  let s:dein_dir = expand('~/.cache/dein/nvim')
else
  let s:dein_dir = expand('~/.cache/dein/vim')
endif
let s:dein_repo_dir = s:dein_dir . '/repos/github.com/Shougo/dein.vim'

if &runtimepath !~# '/dein.vim'
  if !isdirectory(s:dein_repo_dir)
    execute '!git clone https://github.com/Shougo/dein.vim' s:dein_repo_dir
  endif
  execute 'set runtimepath^=' . s:dein_repo_dir
endif

if dein#load_state(s:dein_dir)
  call dein#begin(s:dein_dir)

  call dein#add('kana/vim-operator-replace')
  call dein#add('kana/vim-operator-user')

  " only vim
  if has("nvim")
    call dein#add('neoclide/coc.nvim', {'rev': 'release'})
  else
    call dein#add('prabirshrestha/vim-lsp')
    call dein#add('mattn/vim-lsp-settings', {'merged': 0})
  endif

  " syntax
  call dein#add('cespare/vim-toml')
  call dein#add('jparise/vim-graphql')
  call dein#add('plasticboy/vim-markdown')
  call dein#add('tokorom/vim-review')
  call dein#add('mg979/vim-xtabline')

  " colorscheme
  call dein#add('cocopon/iceberg.vim')

  " for development
  call dein#add('t9md/vim-choosewin')
  call dein#add('Shougo/dein.vim')
  call dein#add('andymass/vim-matchup')
  call dein#add('cohama/lexima.vim')
  call dein#add('fatih/vim-go')
  call dein#add('junegunn/fzf', {'merged': 0})
  call dein#add('junegunn/fzf.vim', {'depends': 'fzf'})
  call dein#add('kshenoy/vim-signature')
  call dein#add('lambdalisue/fern.vim')
  call dein#add('lambdalisue/gina.vim')
  call dein#add('markonm/traces.vim')
  call dein#add('mattn/emmet-vim')
  call dein#add('mattn/sonictemplate-vim')
  call dein#add('mattn/vim-goimports')
  call dein#add('simeji/winresizer')
  call dein#add('skanehira/command.vim')
  call dein#add('skanehira/denops-germanium.vim')
  call dein#add('skanehira/denops-docker.vim')
  call dein#add('skanehira/getpr.vim')
  call dein#add('skanehira/translate.vim')
  call dein#add('thinca/vim-quickrun')
  call dein#add('tyru/open-browser-github.vim')
  call dein#add('tyru/open-browser.vim')
  call dein#add('mbbill/undotree')
  call dein#add('rust-lang/rust.vim')
  call dein#add('airblade/vim-gitgutter')
  call dein#add('gelguy/wilder.nvim')

  " for documentation
  call dein#add('glidenote/memolist.vim')
  call dein#add('godlygeek/tabular')
  call dein#add('gyim/vim-boxdraw')
  call dein#add('mattn/vim-maketable')
  call dein#add('previm/previm')
  call dein#add('shinespark/vim-list2tree')
  call dein#add('skanehira/gyazo.vim')
  call dein#add('skanehira/preview-uml.vim')
  call dein#add('hashivim/vim-terraform')
  call dein#add('tyru/eskk.vim')
  call dein#add('tyru/skkdict.vim')
  call dein#add('vim-jp/vimdoc-ja')

  " for develop vim/neovim plugin
  call dein#add('LeafCage/vimhelpgenerator')
  call dein#add('lambdalisue/vital-Whisky', {'merged': 0})
  call dein#add('tweekmonster/helpful.vim')
  call dein#add('vim-jp/vital.vim', {'merged': 0})

  " other
  call dein#add('basyura/TweetVim')
  call dein#add('basyura/twibill.vim')
  call dein#add('itchyny/lightline.vim')
  call dein#add('itchyny/vim-winfix')

  " end settings
  call dein#end()
  call dein#save_state()
endif

if dein#check_install()
  call dein#install()
endif

function! DeinClean() abort
  let s:removed_plugins = dein#check_clean()
  if len(s:removed_plugins) > 0
    call map(s:removed_plugins, "delete(v:val, 'rf')")
    call dein#recache_runtimepath()
  endif
endfunction

command! CleanPlugins call DeinClean()
" }}}

" {{{ translate.vim
nmap gr <Plug>(Translate)
vmap gt <Plug>(VTranslate)
" }}}

" {{{ fern.vim
function! s:fern_init() abort
  nnoremap <buffer> <silent> q :q<CR>
  map <buffer> <silent> <C-x> <Plug>(fern-action-open:split)
  map <buffer> <silent> <C-v> <Plug>(fern-action-open:vsplit)
  map <buffer> <silent> <C-t> <Plug>(fern-action-tcd)
endfunction

let g:fern#drawer_keep = 1
let g:fern#default_hidden = 1
let g:fern#default_exclude = '.git$'

augroup fern-settings
  au!
  au FileType fern call s:fern_init()
augroup END

nnoremap <silent> <Leader>f :Fern . -drawer<CR>
" }}}

" {{{ gina.vim
call gina#custom#mapping#nmap(
      \ 'status', 'gp',
      \ ':<C-u>terminal git push<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'gP',
      \ ':<C-u>terminal git pull<CR>',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'gr',
      \ ':<C-u>terminal gh pr create -d<CR>',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'gl',
      \ ':<C-u>terminal git pull<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'cm',
      \ ':<C-u>Gina commit<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'ca',
      \ ':<C-u>Gina commit --amend<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'dp',
      \ '<Plug>(gina-patch-oneside-tab)',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'ga',
      \ '--',
      \ {'silent': 1},
      \)
call gina#custom#mapping#vmap(
      \ 'status', 'ga',
      \ '--',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'log', 'dd',
      \ '<Plug>(gina-changes-of)',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'branch', 'n',
      \ '<Plug>(gina-branch-new)',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'branch', 'D',
      \ '<Plug>(gina-branch-delete)',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ '/.*', 'q',
      \ ':<C-u>bw!<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'blame', '<C-o>',
      \ ':<C-u>call GinaOpenPR()<CR>',
      \ {'silent': 1},
      \)
call gina#custom#command#option('log', '--opener', 'new')
call gina#custom#command#option('status', '--opener', 'new')
call gina#custom#command#option('branch', '--opener', 'new')

let s:open = 'open'
if has('linux')
  let s:open = 'xdg-open'
elseif has('win64')
  let s:open = 'cmd /c start'
endif

function! GinaOpenPR() abort
  let can = gina#action#candidates()
  let url = trim(system(printf('%s %s', 'getpr', can[0].rev)))
  call system(printf('%s %s', s:open, url))
endfunction

nnoremap <silent> gs :Gina status -s<CR>
nnoremap <silent> gl :Gina log<CR>
nnoremap <silent> gm :Gina blame<CR>
nnoremap <silent> gb :Gina branch<CR>
nnoremap <silent> gu :Gina browse --exact --yank :<CR>
vnoremap <silent> gu :Gina browse --exact --yank :<CR>
" }}}

" {{{ quickrun.vim
nnoremap <Leader>q :QuickRun -runner terminal<CR>
let g:quickrun_config = {
      \ 'typescript': {
      \ 'command': 'deno',
      \ 'cmdopt': '--no-check --unstable --allow-all',
      \ 'exec': ['NO_COLOR=1 %C run %o %s'],
      \ 'type': 'typescript',
      \ },
      \ 'deno_test': {
      \ 'command': 'deno',
      \ 'cmdopt': 'test --no-check --unstable --allow-all',
      \ 'tempfile': '%{printf("%s_test.ts", tempname())}',
      \ 'exec': ['NO_COLOR=1 %C test %o %s'],
      \ 'type': 'typescript',
      \ },
      \ 'deno_terminal': {
      \ 'command': 'deno',
      \ 'cmdopt': '--no-check --unstable --allow-all',
      \ 'exec': ['%C run %o %s'],
      \ 'type': 'typescript',
      \ 'runner': 'terminal',
      \ },
      \ 'go_bench': {
      \ 'command': 'go',
      \ 'tempfile': '%{printf("%s_test.go", tempname())}',
      \ 'exec': ['%C test -bench . -benchmem'],
      \ 'type': 'go',
      \ 'runner': 'terminal',
      \ },
      \ 'go_test': {
      \ 'command': 'go',
      \ 'tempfile': '%{printf("%s_test.go", tempname())}',
      \ 'exec': ['%C test -v . '],
      \ 'type': 'go',
      \ },
      \ }
augroup quickrun-config
  au!
  au FileType quickrun nnoremap <buffer> q :bw!<CR>
augroup END
" }}}

" {{{ germanium.vim
map gi <Plug>(Germanium)
xmap gi <Plug>(Germanium)
" }}}

" vim-go settings {{{
let g:go_fmt_autosave = 0
let g:go_imports_autosave = 0
let g:go_fmt_command = 'goimports' " ãƒ•ã‚¡ã‚¤ãƒ«ä¿å­˜æ™‚go importã‚’å®Ÿè¡Œã™ã‚‹
let g:go_gopls_enabled = 0 " goplsã‚’æœ‰åŠ¹åŒ–
let g:go_def_mapping_enabled = 0 " vim-lspã‚’ä½¿ç”¨ã™ã‚‹ã®ã§ã€vim-goã®`Ctrl+]`ã‚’ç„¡åŠ¹ã«ã™ã‚‹
let g:go_template_autocreate = 0 " ãƒ†ãƒ³ãƒ—ãƒ¬ãƒ¼ãƒˆä½œæˆã‚’ç„¡åŠ¹åŒ–
let g:go_def_reuse_buffer = 1 " ã™ã§ã«é–‹ã„ã¦ã„ã‚‹ãƒãƒƒãƒ•ã‚¡ã«å®šç¾©ã‚¸ãƒ£ãƒ³ãƒ—ã™ã‚‹
"let g:go_fold_enable = ['block', 'import', 'varconst', 'package_comment']
" }}}

" fzf settings {{{
let g:fzf_layout = { 'down': '50%' }
nnoremap <C-P> :Files<CR>
" }}}

" lsp settings {{{
" coc.nvim
if has("nvim")
  " GoTo code navigation.
  nmap <silent> <C-]> <Plug>(coc-definition)
  nmap <silent> <C-t> :<C-u>call CocActionAsync('jumpDefinition', 'vsplit')<CR>
  nmap <silent> <Leader>gi <Plug>(coc-implementation)
  nmap <silent> <Leader>gr <Plug>(coc-references)

  " Use K to show documentation in preview window.
  nnoremap <silent> K :call <SID>show_documentation()<CR>

  command! LspRename :call CocActionAsync('rename')
  command! LspReferences :call CocActionAsync('jumpReferences')
  command! LspFormat :call CocActionAsync('format')

  " use manual completion
  inoremap <silent><expr> <C-x><C-o> coc#refresh()

  function! s:show_documentation()
    if (index(['vim','help'], &filetype) >= 0)
      execute 'h '.expand('<cword>')
    elseif (coc#rpc#ready())
      call CocActionAsync('doHover')
    else
      execute '!' . &keywordprg . " " . expand('<cword>')
    endif
  endfunction

  let g:coc_global_extensions = [
        \ 'coc-yaml',
        \ 'coc-vimlsp',
        \ 'coc-vetur',
        \ 'coc-tsserver',
        \ 'coc-sql',
        \ 'coc-sh',
        \ 'coc-rust-analyzer',
        \ 'coc-json',
        \ 'coc-deno',
        \ 'coc-eslint',
        \ 'coc-go',
        \ 'coc-spell-checker',
        \ ]

  call coc#config('diagnostic', {
        \ 'errorSign': 'ğŸ¦',
        \ 'warningSign': 'ğŸ’©',
        \ })

  call coc#config('coc.preferences.formatOnType', 'true')
  call coc#config('coc.preferences.useQuickfixForLocations', 'true')
  " disable auto completion
  call coc#config('suggest.autoTrigger', 'none')
  call coc#config('coc.preferences', {
        \ 'formatOnSaveFiletypes': [
          \ 'css',
          \ 'js',
          \ 'javascriptreact',
          \ 'ts',
          \ 'typescriptreact',
          \ 'typescript',
          \ 'html',
          \ 'scss',
          \ 'sass',
          \ 'json',
          \ 'rust',
          \ 'sql',
          \ 'bash',
          \]})

  call coc#config('languageserver', {
        \ 'terraform': {
          \ 'command': 'terraform-ls',
          \ 'trace.server': 'verbose',
          \ 'filetypes': ['tf', 'terraform'],
          \ 'args': ['serve'],
        \ },
        \ 'efm': {
          \ 'command': 'efm-langserver',
          \ 'args': [],
          \ 'trace.server': 'verbose',
          \ 'filetypes': ['markdown']
          \ },
        \})
" vim-lsp
else
  let g:lsp_diagnostics_signs_error = {'text': 'ğŸ¦'}
  let g:lsp_diagnostics_signs_warning = {'text': 'ğŸŒ'}
  if !has('nvim')
    let g:lsp_diagnostics_float_cursor = 1
  endif
  let g:lsp_log_file = ''

  nmap <Leader>ho <plug>(lsp-hover)
  nnoremap <silent> <C-]> :LspDefinition<CR>

  let g:lsp_settings = {
        \ 'gopls': {
        \  'workspace_config': {
        \    'usePlaceholders': v:true,
        \    'analyses': {
        \      'fillstruct': v:true,
        \    },
        \  },
        \  'initialization_options': {
        \    'usePlaceholders': v:true,
        \    'analyses': {
        \      'fillstruct': v:true,
        \    },
        \  },
        \ },
        \ 'eslint-language-server': {
        \   'allowlist': ['javascript', 'typescript', 'vue'],
        \ },
        \ 'efm-langserver': {
        \   'disabled': 0,
        \   'allowlist': ['markdown'],
        \  }
        \}

  let g:lsp_settings_filetype_typescript = ['typescript-language-server', 'eslint-language-server', 'deno']

  function! s:on_lsp_buffer_enabled() abort
    setlocal completeopt=menu
    setlocal omnifunc=lsp#complete
  endfunction

  function! s:autofmt() abort
    if &ft == 'typescript'
      LspDocumentFormatSync
    endif
  endfunction

  augroup lsp_autofmt
    au!
    au BufWrite * call s:autofmt()
  augroup END

  augroup lsp_install
    au!
    au User lsp_buffer_enabled call s:on_lsp_buffer_enabled()
  augroup END

  "augroup vim_lsp_golangci_lint_langserver
  "  au!
  "  autocmd User lsp_setup call lsp#register_server({
  "      \ 'name': 'golangci-lint-langserver',
  "      \ 'cmd': {server_info->['golangci-lint-langserver']},
  "      \ 'initialization_options': {'command': ['golangci-lint', 'run', 'json']},
  "      \ 'allowlist': ['go'],
  "      \ })
  "augroup END
endif
" }}}

" vim-markdown {{{
let g:vim_markdown_folding_disabled = 1
" }}}

" emmet {{{
let g:user_emmet_install_global = 0
let g:user_emmet_settings = {
      \   'variables': {
      \     'lang' : 'ja'
      \   }
      \ }
let g:user_emmet_leader_key = '<C-g>'

augroup emmet
  au!
  au FileType vue,html,css EmmetInstall
  au FileType vue,html,css imap <buffer> <C-f> <plug>(emmet-expand-abbr)
augroup END
" }}}

" sonictemplate.vim {{{
let g:sonictemplate_vim_template_dir = ["~/.vim/template"]
let g:sonictemplate_author = 'skanehira'
let g:sonictemplate_license = 'MIT'
let g:sonictemplate_vim_template_dir = expand('~/.vim/sonictemplate')
imap <silent> <C-l> <plug>(sonictemplate-postfix)
" }}}

" vimhelpgenarator {{{
let g:vimhelpgenerator_version = ''
let g:vimhelpgenerator_author = 'Author: skanehira <sho19921005@gmail.com>'
let g:vimhelpgenerator_uri = 'https://github.com/skanehira/'
let g:vimhelpgenerator_defaultlanguage = 'en'
" }}}

" {{{ MemoList
let g:memolist_fzf = 1
" }}}

" {{{ docker.vim
let g:docker_use_tmux = 1
let g:docker_plugin_version_check = 0
" }}}

" {{{ preview.vim
let g:previm_open_cmd = 'open'
let g:previm_plantuml_imageprefix = 'http://localhost:8888/png/'
let g:previm_enable_realtime = 1
" }}}

" {{{ preview-markdown.vim
let g:preview_markdown_vertical = 1
let g:preview_markdown_auto_update = 1
" }}}

"{{{ gist-vim
let g:gist_post_private = 1
let g:gist_list_vsplit = 1
"}}}

" {{{ tweetvim
let g:tweetvim_display_time = 1
let g:tweetvim_open_buffer_cmd = 'tabnew'
nnoremap <silent> <Leader>th :<C-u>TweetVimHomeTimeline<CR>
nnoremap <silent> <Leader>ts :<C-u>TweetVimSay<CR>
" }}}

" {{{ gh.vim
let g:gh_open_issue_on_create = 1
function! s:gh_map_apply() abort
  if !exists('g:loaded_gh')
    return
  endif
  call gh#map#add('gh-buffer-issue-list', 'nmap', 'e', '<Plug>(gh_issue_edit)')
  call gh#map#add('gh-buffer-issue-list', 'nmap', 'gm', '<Plug>(gh_issue_open_comment)')
  call gh#map#add('gh-buffer-issue-list', 'nmap', 'y', '<Plug>(gh_issue_url_yank)')
  call gh#map#add('gh-buffer-issue-comment-list', 'nmap', 'n', '<Plug>(gh_issue_comment_new)')
  call gh#map#add('gh-buffer-issue-comment-list', 'nmap', 'e', '<Plug>(gh_issue_comment_edit)')
  call gh#map#add('gh-buffer-issue-edit', 'nmap', 'gm', '<Plug>(gh_issue_comment_open_on_issue)')
  call gh#map#add('gh-buffer-pull-list', 'nmap', 'y', '<Plug>(gh_pull_url_yank)')
  call gh#map#add('gh-buffer-project-list', 'nmap', 'y', '<Plug>(gh_project_url_yank)')
  call gh#map#add('gh-buffer-project-column-list', 'nmap', 'y', '<Plug>(gh_projects_card_url_yank)')
  call gh#map#add('gh-buffer-project-column-list', 'nmap', 'o', '<Plug>(gh_projects_card_open)')
  call gh#map#add('gh-buffer-action-list', 'nmap', 'o', '<Plug>(gh_actions_open_browser)')
  call gh#map#add('gh-buffer-action-list', 'nmap', 'y', '<Plug>(gh_actions_yank_url)')
  call gh#map#add('gh-buffer-gist-list', 'nmap', 'e', '<Plug>(gh_gist_edit_file)')
  call gh#map#add('gh-buffer-gist-list', 'nmap', 'y', '<Plug>(gh_gist_list_yank)')
  call gh#map#add('gh-buffer-bookmark-list', 'nmap', '<CR>', ':e <C-r>=getline(".")<CR><CR>')
endfunction

augroup gh-maps
  au!
  au VimEnter * call <SID>gh_map_apply()
augroup END

nnoremap <silent> gh :new gh://bookmarks<CR>
" }}}

" {{{ vim-operator-replace
if !has('mac') && !has('nvim')
  vmap p <Plug>(operator-replace)
endif
" }}}

" {{{ getpr.vim
map go <Plug>(getpr-open)
map gy <Plug>(getpr-yank)
" }}}

" {{{ gyazo.vim
let g:gyazo_insert_markdown_url = 1
nmap gup <Plug>(gyazo-upload)
" }}}

" {{{ vim-choosewin
nmap <C-f> <Plug>(choosewin)
" }}}

" {{{ lightline
let g:lightline = {
      \ 'colorscheme': 'wombat',
      \ 'active': {
      \ 'left': [ ['mode', 'paste'], ['readonly', 'branchName', 'filepath', 'modified'] ]
      \ },
      \ 'component_function':{
      \ 'filepath': 'FilePath',
      \ },
      \ }

function! FilePath()
  if winwidth(0) > 90
    return expand("%:s")
  else
    return expand("%:t")
  endif
endfunction
" }}}

" {{{ rust.vim
if !has("nvim")
  let g:rustfmt_autosave = 1
endif
" }}}

" {{{ command.vim
nmap c: <Plug>(command_buffer_open)
" }}}
" }}}

syntax enable " ã‚·ãƒ³ã‚¿ãƒƒã‚¯ã‚¹ã‚’æœ‰åŠ¹ã«ã™ã‚‹
filetype plugin indent on " ãƒ•ã‚¡ã‚¤ãƒ«å½¢å¼åˆ¥ãƒ—ãƒ©ã‚°ã‚¤ãƒ³ã¨ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆã‚’æœ‰åŠ¹ã«ã™ã‚‹

" ã‚«ãƒ©ãƒ¼ã‚¹ã‚­ãƒ¼ãƒ ã‚’ä½¿ã† {{{
colorscheme iceberg
" icebergã¯ã‚¿ãƒ¼ãƒŸãƒŠãƒ«ã‚’16è‰²ã«è¨­å®šã™ã‚‹ã®ã‚’å›é¿
if !has('nvim')
  augroup iceberg
    au!
    au ColorScheme * unlet g:terminal_ansi_colors
  augroup END
endif
set background=dark
"let g:tokyonight_disable_italic_comment = 1
"colorscheme tokyonight
" }}}

" ã‚ªãƒ—ã‚·ãƒ§ãƒ³ {{{
set encoding=utf-8
set t_ut=
set fileencodings=utf-8,iso-2022-jp,euc-jp,sjis
set fileformats=unix,dos,mac
set belloff=all " ãƒŸãƒ¥ãƒ¼ãƒˆ
set backspace=2 " ãƒãƒƒã‚¯ã‚¹ãƒšãƒ¼ã‚¹ã¨Ctrl+hã§å‰Šé™¤ã‚’æœ‰åŠ¹ã«ã™ã‚‹
set smartindent autoindent " æ”¹è¡Œæ™‚è‡ªå‹•ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆ
set incsearch " ã‚¤ãƒ³ã‚¯ãƒªãƒ¡ãƒ³ãƒˆã‚µãƒ¼ãƒã‚’æœ‰åŠ¹ã«ã™ã‚‹
set ignorecase " æ¤œç´¢æ™‚å¤§æ–‡å­—å°æ–‡å­—ã‚’åŒºåˆ¥ã—ãªã„
set smartcase " æ¤œç´¢æ™‚ã«å¤§æ–‡å­—ã‚’å…¥åŠ›ã—ãŸå ´åˆignorecaseãŒç„¡åŠ¹ã«ãªã‚‹
set hlsearch " ãƒã‚¤ãƒ©ã‚¤ãƒˆã‚µãƒ¼ãƒã‚’æœ‰åŠ¹ã«ã™ã‚‹
set undolevels=1000 " undoã§ãã‚‹æœ€å¤§æ•°
set scrolloff=100 " ã‚«ãƒ¼ã‚½ãƒ«ãŒå¸¸ã«ä¸­å¤®ã«æ¥ã‚‹ã‚ˆã†ã«ã™ã‚‹
set lazyredraw ttyfast " ãƒã‚¯ãƒ­ã§åŠ¹æœç™ºæ®
set synmaxcol=256 "ä¸€è¡ŒãŒé•·ã„ãƒ•ã‚¡ã‚¤ãƒ«ã‚’syntaxã‚’åˆ¶å¾¡ã™ã‚‹ã“ã¨ã§è»½ãã™ã‚‹
set cursorline " ã‚«ãƒ¼ã‚½ãƒ«ãƒ©ã‚¤ãƒ³ã‚’è¡¨ç¤ºã™ã‚‹
set wildmenu " wildmenuã‚’æœ‰åŠ¹ã«ã™ã‚‹
set virtualedit=block " çŸ©å½¢é¸æŠæ™‚ã«æ–‡å­—ã®ç„¡ã„ã¨ã“ã‚ã¾ã§é¸æŠç¯„å›²ã‚’åºƒã’ã‚‹
set helplang=ja " ãƒ˜ãƒ«ãƒ—ã®è¨€èªã‚’æ—¥æœ¬èªå„ªå…ˆã«ã™ã‚‹
set autowrite " ä»–ã®ãƒãƒƒãƒ•ã‚¡ã«ç§»å‹•ã™ã‚‹æ™‚ã«è‡ªå‹•ä¿å­˜
set noswapfile " swapãƒ•ã‚¡ã‚¤ãƒ«ã‚’ä½œæˆã—ãªã„
set showtabline=2 " ã‚¿ãƒ–ã‚’è¡¨ç¤ºã—ãªã„
set noshowmode
set laststatus=2
set list listchars=tab:Â»-,trail:-,extends:Â»,precedes:Â«,nbsp:% " listã®è¨­å®š
if !has('nvim')
  set nrformats+=unsigned " æ•°å€¤ã®åŠ æ¸›ç®—ã‚’è€ƒæ…®
  set cursorlineopt=line " è¡Œã®ã¿ãƒã‚¤ãƒ©ã‚¤ãƒˆ
  set sessionoptions=blank,buffers,curdir,folds,help,tabpages,winsize,terminal " ã‚»ãƒƒã‚·ãƒ§ãƒ³ã§ä¿å­˜ã™ã‚‹å¯¾è±¡
endif
if has("mac") | set clipboard+=unnamed | else | set clipboard^=unnamedplus | endif " ã‚¯ãƒªãƒƒãƒ—ãƒœãƒ¼ãƒ‰ã‚’å…±æœ‰
set diffopt=vertical

" æ‹¡å¼µå­ã”ã¨ã®ã‚¤ãƒ³ãƒ‡ãƒ³ãƒˆè¨­å®š {{{
set tabstop=2 shiftwidth=2 softtabstop=2

augroup fileTypeIndent
  au!
  au FileType go setlocal tabstop=4 shiftwidth=4
  au FileType vim setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType php setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType html setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType javascript setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType typescript setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType vue  setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType yaml setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType json setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType sh setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType fish setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType rust setlocal tabstop=4 softtabstop=4 shiftwidth=4 expandtab
  au FileType markdown setlocal tabstop=2 shiftwidth=2
augroup END
" }}}

" {{{ ã‚·ãƒ³ã‚¿ãƒƒã‚¯ã‚¹ã‚’ã‚¯ãƒªã‚¢
augroup fileTypeSyntaxClear
  au!
  au FileType go syntax clear
  au FileType vim syntax clear
  au FileType html syntax clear
  au FileType javascript syntax clear
  au FileType typescript syntax clear
  au FileType vue  syntax clear
augroup END
" }}}

" grepã—ãŸçµæœã‚’quickfixã«è¡¨ç¤ºã™ã‚‹ {{{
augroup grepwindow
  au!
  au QuickFixCmdPost *grep* cwindow
augroup END
" }}}

" ã‚«ãƒ¼ã‚½ãƒ«ãƒ©ã‚¤ãƒ³ã®ä½ç½®ã‚’ä¿å­˜ã™ã‚‹ {{{
augroup cursorlineRestore
  au!
  au BufReadPost *
        \ if line("'\"") > 0 && line("'\"") <= line("$") |
        \   exe "normal! g'\"" |
        \ endif
augroup END
" }}}

" undoã®ä¿å­˜å…ˆ {{{
if has('persistent_undo')
  let undo_path = expand('~/.vim/undo')
  " ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªãŒå­˜åœ¨ã—ãªã„å ´åˆã¯ä½œæˆ
  if !isdirectory(undo_path)
    call mkdir(undo_path, 'p')
  endif
  exe 'set undodir=' .. undo_path
  set undofile
endif
" }}}
" }}}

" ã‚­ãƒ¼ãƒãƒƒãƒ— {{{

" ^ã¨0ã‚’0ã«çµ±ä¸€
nnoremap <expr> 0 getline('.')[0 : col('.') - 2] =~# '^\s\+$' ? '0' : '^'

" *ã§ã‚«ãƒ¼ã‚½ãƒ«ã‚’ç§»å‹•ã—ãªã„ã‚ˆã†ã«ã™ã‚‹
noremap * *N

" ãƒ•ã‚¡ã‚¤ãƒ«ä¿å­˜ã¨çµ‚äº†
nnoremap <Leader>w :w<CR>

" æ¤œç´¢
nnoremap <C-G><C-G> :Grep <C-R><C-W><CR>

" ç½®æ›
nnoremap <Leader>re :%s;\<<C-R><C-W>\>;g<Left><Left>;

" ãƒã‚¤ãƒ©ã‚¤ãƒˆã‚’å‰Šé™¤ã™ã‚‹
nnoremap <silent> <Esc><Esc> :nohlsearch<CR>

" vimrcã‚’é–‹ã
nnoremap <Leader>. :new ~/.vimrc<CR>
nnoremap <Leader>s :exe "source" expand("%")<CR>

" ãƒ†ã‚­ã‚¹ãƒˆã‚ªãƒ–ã‚¸ã‚§ã‚¯ãƒˆã‚­ãƒ¼ãƒãƒƒãƒ”ãƒ³ã‚° {{{
onoremap 8 i(
onoremap 2 i"
onoremap 7 i'
onoremap @ i`
onoremap [ i[
onoremap { i{

onoremap a8 a(
onoremap a2 a"
onoremap a7 a'
onoremap a@ a`
onoremap a[ a[
onoremap a{ a{

" visual
nnoremap v8 vi(
nnoremap v2 vi"
nnoremap v7 vi'
nnoremap v@ vi`
nnoremap v[ vi[
nnoremap v{ vi{

nnoremap va8 va(
nnoremap va2 va"
nnoremap va7 va'
nnoremap va@ va`
nnoremap va[ va[
nnoremap va{ va{
" }}}

" è¡Œå…ˆé ­ã¨è¡Œæœ«
noremap H ^
noremap L g_

" ã‚¿ãƒ–åˆ‡ã‚Šæ›¿ãˆ
nnoremap <C-l> gt
nnoremap <C-h> gT

" ã‚¿ãƒ¼ãƒŸãƒŠãƒ«é–¢é€£ {{{
tnoremap <C-]> <C-\><C-n>
if has('nvim')
  nnoremap <Leader>tm :new term<CR>
  function! s:term_start(cmd) abort
    call termopen(a:cmd)
  endfunction
else
  nnoremap <Leader>tm :term<CR>
  function! s:term_start(cmd) abort
    let old = bufnr()
    exe printf('term ++curwin ++shell %s', a:cmd)
    exe "bw!" old
    nnoremap <buffer> <silent> <CR> :bw!<CR>
  endfunction
endif
nnoremap <Leader>ttm :tabnew term<CR>

function! s:termopen() abort
  let bufname = bufname()
  let parts = split(bufname, ' ')
  let cmd = &shell
  if len(parts) > 1
    " like :new term [command] [args]
    let cmd = join(parts[1:], " ")
  endif
  call s:term_start(cmd)
endfunction

if has('nvim')
  augroup neovim-terminal
    au!
    au TermOpen * startinsert
  augroup END
else
  set termwinkey=<C-]>
  " ã‚¿ãƒ¼ãƒŸãƒŠãƒ«ãƒãƒ¼ãƒãƒ«ãƒ¢ãƒ¼ãƒ‰
endif

augroup terminal
  au!
  au BufNewFile term* ++nested call s:termopen()
augroup END
" }}}

" æ”¹è¡Œ
nnoremap <C-j> o<ESC>
nnoremap <C-k> O<ESC>
nnoremap o A<CR>

" Emacsé¢¨ã®ã‚­ãƒ¼ãƒãƒƒãƒ—
imap <C-h> <BS>
inoremap <C-k> <C-o>C
inoremap <silent> <C-f> <Right>
inoremap <silent> <C-b> <Left>
inoremap <silent> <C-e> <C-o>A
inoremap <silent> <C-a> <C-o>I

" C-v ã§ãƒšãƒ¼ã‚¹ãƒˆ
inoremap <expr> <C-v> printf('<C-r><C-o>%s', has('linux') \|\| has('unix') ? '+' : '*')
cnoremap <expr> <C-v> printf('<C-r><C-o>%s', has('linux') \|\| has('unix') ? '+' : '*')

" å›²ã†
nnoremap <silent> gw[ cw``<Esc>P
vnoremap <silent> gw[ c``<Esc>P

" ãƒ˜ãƒ«ãƒ—
augroup help-mapping
  au!
  au FileType help nnoremap <buffer> <silent>q :bw!<CR>
augroup END
" }}}

" ã‚³ãƒãƒ³ãƒ‰ãƒ©ã‚¤ãƒ³ã§å˜èªç§»å‹• {{{
cnoremap <c-b> <Left>
cnoremap <c-f> <Right>
cnoremap <c-a> <Home>
" }}}

" Better <C-n>/<C-p> in Command {{{
cnoremap <C-p> <Up>
cnoremap <C-n> <Down>
cnoremap <Up> <C-p>
cnoremap <Down> <C-n>
" }}}

" {{{ ãƒªãƒã‚¸ãƒˆãƒªã«ç§»å‹•
function! s:cd_repo(shell, repo) abort
  exe 'lcd' trim(system('ghq root')) .. '/' .. a:repo
  pwd
endfunction

function! s:repo(cb) abort
  if executable('ghq') && exists('*fzf#run()') && executable('fzf')
    call fzf#run({
          \ 'source': systemlist('ghq list'),
          \ 'sink': a:cb,
          \ 'down': '40%'},
          \ )
  else
    echo "doesn't installed ghq or fzf.vim(require fzf)"
  endif
endfunction

command! Repo call s:repo(function('s:cd_repo', [&shell]))
" }}}

" {{{ ã‚¨ãƒ©ãƒ¼ãƒ¡ãƒƒã‚»ãƒ¼ã‚¸å‡ºåŠ›
function! s:echo_err(message) abort
  echohl ErrorMsg
  redraw
  echo a:message
  echohl None
endfunction
" }}}

" {{{ ãƒ‡ã‚£ãƒ¬ã‚¯ãƒˆãƒªè‡ªå‹•ç”Ÿæˆ
function! s:auto_mkdir(dir)
  if !isdirectory(a:dir)
    call mkdir(iconv(a:dir, &encoding, &termencoding), 'p')
  endif
endfunction
augroup auto-mkdir
  au!
  au BufWritePre * call s:auto_mkdir(expand('<afile>:p:h'))
augroup END
" }}}

" {{{ lazygit
nnoremap <silent> gp :call system('fish -c "tmuxpopup lazygit"')<CR>
" }}}

" {{{ golangci-lint
function! s:golangci(...) abort
  if !executable('golangci-lint')
    call s:echo_err('golangci-lint does not installed')
    return
  endif

  if a:0 > 0
    let opts = a:000
  else
    let opts = get(g:, 'golangci_opts', ['misspell', 'unparam', 'stylecheck',
          \ 'gosec', 'prealloc', 'gocritic', 'gomnd', 'unconvert'])
  endif

  if get(g:, 'golangci_disable_all', 0)
    let cmd = printf('golangci-lint run --disable-all --print-issued-lines=false --enable=%s', join(opts, ','))
  else
    let cmd = printf('golangci-lint run --print-issued-lines=false --enable=%s', join(opts, ','))
  endif
  cexpr system(cmd) | cw
endfunction
command! -nargs=* GolangCI call <SID>golangci(<f-args>)
" }}}

" {{{ grep
function! s:grep(word) abort
  let cmd = printf('ag %s', a:word)
  lex system(cmd) | lw
endfunction

command! -nargs=1 Grep call <SID>grep(<q-args>)
" }}}

" {{{ zenn
function! s:create_zenn_article(article_name) abort
  let date = strftime("%Y-%m-%d")
  let slug = date . "-" . a:article_name
  call system("npx zenn new:article --emoji ğŸ¦ --slug " . slug )
  let article_path = "articles/" . slug . ".md"
  exe "edit " . article_path
endfunction

command! -nargs=1 ZennCreateArticle call <SID>create_zenn_article(<f-args>)
" }}}

" {{{ goimports
augroup goimports
  function! s:goimports() abort
    let pos = getcurpos()
    %!goimports
    call setpos('.', pos)
  endfunction
  au!
  au! FileType go nnoremap <silent> <Leader>i :call <SID>goimports()<CR>
augroup END
" }}}

" {{{ preview swagger
function! s:swagger_preview(file) abort
  if !executable('spr')
    call s:echo_err('spr does not installed')
    return
  endif

  let s:swagger_preview_jobid = job_start(['spr', a:file])
endfunction

function! s:swagger_preview_stop() abort
  call job_stop(s:swagger_preview_jobid)
endfunction

command! -nargs=1 -complete=file SwaggerPreview call <SID>swagger_preview(<f-args>)
command! SwaggerPreviewStop call <SID>swagger_preview_stop()
" }}}

" {{{ eskk.vim
let g:eskk#kakutei_when_unique_candidate = 1 "æ¼¢å­—å¤‰æ›ã—ãŸæ™‚ã«å€™è£œãŒ1ã¤ã®å ´åˆã€è‡ªå‹•çš„ã«ç¢ºå®šã™ã‚‹
let g:eskk#enable_completion = 0             "neocompleteã‚’å…¥ã‚Œãªã„ã¨ã€1ã«ã™ã‚‹ã¨å‹•ä½œã—ãªããªã‚‹ãŸã‚0æ¨å¥¨
let g:eskk#egg_like_newline = 1              "æ¼¢å­—å¤‰æ›ã‚’ç¢ºå®šã—ã¦ã‚‚æ”¹è¡Œã—ãªã„ã€‚

let g:eskk#directory = "~/.eskk"
let g:eskk#large_dictionary = {
      \ 'path': "~/.eskk/SKK-JISYO.L",
      \ 'sorted': 1,
      \ }
" }}}

" {{{ wilder.nvim
call wilder#setup({
      \ 'modes': [':', '/'],
      \ 'next_key': '<C-n>',
      \ 'previous_key': '<C-p>',
      \ 'accept_key': '<Down>',
      \ 'reject_key': '<Up>',
      \ })

call wilder#set_option('renderer', wilder#popupmenu_renderer({
      \ 'highlighter': wilder#basic_highlighter(),
      \ }))
" }}}

" vim: foldmethod=marker
