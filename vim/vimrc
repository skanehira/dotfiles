"          __
"  __  __ /\_\    ___ ___   _ __   ___
" /\ \/\ \\/\ \ /' __` __`\/\`'__\/'___\
" \ \ \_/ |\ \ \/\ \/\ \/\ \ \ \//\ \__/
"  \ \___/  \ \_\ \_\ \_\ \_\ \_\\ \____\
"   \/__/    \/_/\/_/\/_/\/_/\/_/ \/____/

let g:mapleader = "\<Space>" " Leaderキーをスペースに設定

if has('nvim')
  let g:loaded_python3_provider  = 0
  let g:loaded_shada_plugin      = 1
endif

" Disable unnecessary default plugins
"
let g:loaded_spellfile_plugin   = 1
let g:loaded_tutor_mode_plugin  = 1
let g:loaded_gzip               = 1
let g:loaded_tar                = 1
let g:loaded_tarPlugin          = 1
let g:loaded_zip                = 1
let g:loaded_zipPlugin          = 1
let g:loaded_rrhelper           = 1
let g:loaded_2html_plugin       = 1
let g:loaded_vimball            = 1
let g:loaded_vimballPlugin      = 1
let g:loaded_getscript          = 1
let g:loaded_getscriptPlugin    = 1
let g:loaded_logipat            = 1
let g:loaded_matchparen         = 1
let g:loaded_man                = 1
let g:loaded_netrw              = 1
let g:loaded_netrwPlugin        = 1
let g:loaded_netrwSettings      = 1
let g:loaded_netrwFileHandlers  = 1
let g:loaded_logiPat            = 1
let g:did_install_default_menus = 1
let g:skip_loading_mswin        = 1
let g:did_install_syntax_menu   = 1
let g:plug_shallow = 0

" プラグイン設定 {{{
" {{{ dein.vim settings
let s:dein_dir = has('nvim') ? expand('~/.cache/dein/nvim') : expand('~/.cache/dein/vim')
let s:dein_repo_dir = s:dein_dir . '/repos/github.com/Shougo/dein.vim'
let g:dein#lazy_rplugins = 1

if &runtimepath !~# '/dein.vim'
  if !isdirectory(s:dein_repo_dir)
    execute '!git clone https://github.com/Shougo/dein.vim' s:dein_repo_dir
  endif
  execute 'set runtimepath^=' . s:dein_repo_dir
endif

if dein#load_state(s:dein_dir)
  call dein#begin(s:dein_dir)

  call dein#add('kana/vim-operator-replace')
  call dein#add('kana/vim-operator-user')

  " only vim
  if has("nvim")
    call dein#add('neoclide/coc.nvim', {'rev': 'release'})
  else
    call dein#add('prabirshrestha/vim-lsp')
    call dein#add('mattn/vim-lsp-settings', {'merged': 0})
  endif

  " syntax
  call dein#add('plasticboy/vim-markdown')

  " colorscheme
  call dein#add('cocopon/iceberg.vim')

  " for development
  call dein#add('t9md/vim-choosewin')
  call dein#add('Shougo/dein.vim')
  call dein#add('andymass/vim-matchup')
  call dein#add('cohama/lexima.vim')
  call dein#add('vim-test/vim-test')
  call dein#add('junegunn/fzf', {'merged': 0})
  call dein#add('junegunn/fzf.vim', {'depends': 'fzf'})
  call dein#add('kshenoy/vim-signature')
  call dein#add('lambdalisue/fern.vim')
  call dein#add('lambdalisue/gina.vim')
  call dein#add('lambdalisue/guise.vim')
  call dein#add('markonm/traces.vim')
  call dein#add('mattn/emmet-vim')
  call dein#add('mattn/sonictemplate-vim')
  call dein#add('simeji/winresizer')
  call dein#add('vim-denops/denops.vim')
  call dein#add('skanehira/command.vim')
  call dein#add('skanehira/denops-germanium.vim')
  call dein#add('skanehira/denops-docker.vim')
  call dein#add('skanehira/getpr.vim')
  call dein#add('skanehira/qfopen.vim')
  call dein#add('skanehira/gh.vim')
  call dein#add('thinca/vim-quickrun')
  call dein#add('tyru/open-browser-github.vim')
  call dein#add('tyru/open-browser.vim')
  call dein#add('airblade/vim-gitgutter')
  call dein#add('jparise/vim-graphql')
  call dein#add('Shougo/ddc.vim')
  call dein#add('Shougo/ddc-matcher_head')
  call dein#add('Shougo/ddc-sorter_rank')
  call dein#add('Shougo/ddc-around')
  call dein#add('matsui54/denops-popup-preview.vim')

  " for documentation
  call dein#add('glidenote/memolist.vim')
  call dein#add('godlygeek/tabular')
  call dein#add('gyim/vim-boxdraw')
  call dein#add('mattn/vim-maketable')
  call dein#add('shinespark/vim-list2tree')
  call dein#add('skanehira/gyazo.vim')
  call dein#add('skanehira/denops-translate.vim')
  call dein#add('vim-jp/vimdoc-ja')
  call dein#add('kat0h/bufpreview.vim', {'merged': 0})

  " for develop vim/neovim plugin
  call dein#add('LeafCage/vimhelpgenerator')
  call dein#add('lambdalisue/vital-Whisky', {'merged': 0})
  call dein#add('tweekmonster/helpful.vim')
  call dein#add('vim-jp/vital.vim', {'merged': 0})

  " other
  call dein#add('itchyny/lightline.vim')

  " end settings
  call dein#end()
  call dein#save_state()
endif

if dein#check_install()
  call dein#install()
endif

function! DeinClean() abort
  let s:removed_plugins = dein#check_clean()
  if len(s:removed_plugins) > 0
    echom s:removed_plugins
    call map(s:removed_plugins, "delete(v:val, 'rf')")
    call dein#recache_runtimepath()
  endif
endfunction

command! CleanPlugins call DeinClean()
" }}}

" {{{ translate.vim
nmap gr <Plug>(Translate)
vmap gt <Plug>(VTranslate)
" }}}

" {{{ fern.vim
function! s:fern_init() abort
  nnoremap <buffer> <silent> q :q<CR>
  map <buffer> <silent> <C-x> <Plug>(fern-action-open:split)
  map <buffer> <silent> <C-v> <Plug>(fern-action-open:vsplit)
  map <buffer> <silent> <C-t> <Plug>(fern-action-tcd)
endfunction

let g:fern#drawer_keep = 1
let g:fern#default_hidden = 1
let g:fern#default_exclude = '.git$'

augroup fern-settings
  au!
  au FileType fern call s:fern_init()
augroup END

nnoremap <silent> <Leader>f :Fern . -drawer<CR>
" }}}

" {{{ gina.vim
call gina#custom#mapping#nmap(
      \ 'status', 'gp',
      \ ':<C-u>terminal git push<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'gr',
      \ ':<C-u>terminal gh pr create -d<CR>',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'gl',
      \ ':<C-u>terminal git pull<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'cm',
      \ ':<C-u>Gina commit<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'ca',
      \ ':<C-u>Gina commit --amend<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'dp',
      \ '<Plug>(gina-patch-oneside-tab)',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'status', 'ga',
      \ '--',
      \ {'silent': 1},
      \)
call gina#custom#mapping#vmap(
      \ 'status', 'ga',
      \ '--',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'log', 'dd',
      \ '<Plug>(gina-changes-of)',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'branch', 'n',
      \ '<Plug>(gina-branch-new)',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'branch', 'D',
      \ '<Plug>(gina-branch-delete)',
      \ {'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ '/.*', 'q',
      \ ':<C-u>bw!<CR>',
      \ {'noremap': 1, 'silent': 1},
      \)
call gina#custom#mapping#nmap(
      \ 'blame', '<C-o>',
      \ ':<C-u>call GinaOpenPR()<CR>',
      \ {'silent': 1},
      \)
call gina#custom#command#option('log', '--opener', 'new')
call gina#custom#command#option('status', '--opener', 'new')
call gina#custom#command#option('branch', '--opener', 'new')

let s:open = 'open'
if has('linux')
  let s:open = 'xdg-open'
elseif has('win64')
  let s:open = 'cmd /c start'
endif

function! GinaOpenPR() abort
  let can = gina#action#candidates()
  let url = trim(system(printf('%s %s', 'getpr', can[0].rev)))
  call system(printf('%s %s', s:open, url))
endfunction

nnoremap <silent> gs :Gina status<CR>
nnoremap <silent> gl :Gina log<CR>
nnoremap <silent> gm :Gina blame<CR>
nnoremap <silent> gb :Gina branch<CR>
nnoremap <silent> gu :Gina browse --exact --yank :<CR>
vnoremap <silent> gu :Gina browse --exact --yank :<CR>
" }}}

" {{{ quickrun.vim
nnoremap <Leader>q :QuickRun -runner terminal<CR>
let g:quickrun_config = {
      \ 'typescript': {
      \ 'command': 'deno',
      \ 'cmdopt': '--no-check --unstable --allow-all',
      \ 'exec': ['NO_COLOR=1 %C run %o %s'],
      \ 'type': 'typescript',
      \ },
      \ 'deno_test': {
      \ 'command': 'deno',
      \ 'cmdopt': 'test --no-check --unstable --allow-all',
      \ 'tempfile': '%{printf("%s_test.ts", tempname())}',
      \ 'exec': ['NO_COLOR=1 %C test %o %s'],
      \ 'type': 'typescript',
      \ },
      \ 'deno_terminal': {
      \ 'command': 'deno',
      \ 'cmdopt': '--no-check --unstable --allow-all',
      \ 'exec': ['%C run %o %s'],
      \ 'type': 'typescript',
      \ 'runner': 'terminal',
      \ },
      \ 'go_bench': {
      \ 'command': 'go',
      \ 'tempfile': '%{printf("%s_test.go", tempname())}',
      \ 'exec': ['%C test -bench . -benchmem'],
      \ 'type': 'go',
      \ 'runner': 'terminal',
      \ },
      \ 'go_test': {
      \ 'command': 'go',
      \ 'tempfile': '%{printf("%s_test.go", tempname())}',
      \ 'exec': ['%C test -v . '],
      \ 'type': 'go',
      \ },
      \ }

augroup quickrun-config
  au!
  au FileType quickrun nnoremap <buffer> q :bw!<CR>
augroup END
" }}}

" {{{ germanium.vim
map gi <Plug>(Germanium)
xmap gi <Plug>(Germanium)
" }}}

" fzf settings {{{
let g:fzf_layout = { 'down': '50%' }
nnoremap <C-P> :Files<CR>
" }}}

" lsp settings {{{
" coc.nvim
if has("nvim")
  command! LspRename :call CocActionAsync('rename')
  command! LspReferences :call CocActionAsync('jumpReferences')
  command! LspFormat :call CocActionAsync('format')
  command! LspImport :call CocActionAsync('runCommand', 'editor.action.organizeImport')
  command! GoTagAdd :CocCommand go.tags.add.prompt
  command! GoTagRemove :CocCommand go.tags.remove.prompt

  " GoTo code navigation.
  nmap <silent> <C-]> <Plug>(coc-definition)
  nnoremap <silent> <C-t> :<C-u>call CocActionAsync('jumpDefinition', 'vsplit')<CR>
  nnoremap <silent> <Leader>gi :<C-u>call CocActionAsync('jumpImplementation', 'split')<CR>
  nnoremap <silent> <Leader>gr :<C-u>call CocActionAsync('jumpReferences', 'split')<CR>

  nnoremap <silent> <Leader>i :LspImport<CR>

  " Use K to show documentation in preview window.
  nnoremap <silent> K :call <SID>show_documentation()<CR>

  " use manual completion
  inoremap <silent><expr> <C-x><C-o> coc#refresh()

  function! s:show_documentation()
    if (index(['vim','help'], &filetype) >= 0)
      execute 'h '.expand('<cword>')
    elseif (coc#rpc#ready())
      call CocActionAsync('doHover')
    else
      execute '!' . &keywordprg . " " . expand('<cword>')
    endif
  endfunction

  let g:coc_global_extensions = [
        \ 'coc-yaml',
        \ 'coc-vimlsp',
        \ 'coc-vetur',
        \ 'coc-tsserver',
        \ 'coc-sql',
        \ 'coc-sh',
        \ 'coc-rust-analyzer',
        \ 'coc-json',
        \ 'coc-deno',
        \ 'coc-eslint',
        \ 'coc-go',
        \ ]

  call coc#config('diagnostic', {
        \ 'errorSign': '🦍',
        \ 'warningSign': '💩',
        \ })

  call coc#config('coc.preferences.formatOnType', 'true')
  call coc#config('coc.preferences.useQuickfixForLocations', 'true')
  call coc#config('codeLens.enable', v:true)
  "call coc#config('go.goplsPath', 'gopls')
  call coc#config('go.goplsOptions', {
        \ 'completeUnimported': v:true,
        \ 'codelenses': {
          \ 'test': v:true,
          \ },
          \ })
  " disable auto completion
  call coc#config('suggest.autoTrigger', 'none')
  call coc#config('diagnostic.enable', 'false')
  call coc#config('coc.preferences', {
        \ 'formatOnSaveFiletypes': [
          \ 'go',
          \ 'css',
          \ 'js',
          \ 'javascriptreact',
          \ 'ts',
          \ 'typescriptreact',
          \ 'typescript',
          \ 'html',
          \ 'scss',
          \ 'sass',
          \ 'json',
          \ 'rust',
          \ 'sql',
          \ 'bash',
          \]})

  call coc#config('languageserver', {
        \ 'terraform': {
          \ 'command': 'terraform-ls',
          \ 'trace.server': 'verbose',
          \ 'filetypes': ['tf', 'terraform'],
          \ 'args': ['serve'],
        \ },
        \ 'efm': {
          \ 'command': 'efm-langserver',
          \ 'args': [],
          \ 'trace.server': 'verbose',
          \ 'filetypes': ['markdown']
          \ },
        \})
" vim-lsp
else
  let g:lsp_diagnostics_signs_error = {'text': '🦍'}
  let g:lsp_diagnostics_signs_warning = {'text': '🍌'}
  if !has('nvim')
    let g:lsp_diagnostics_float_cursor = 1
  endif
  let g:lsp_log_file = ''

  nmap <Leader>ho <plug>(lsp-hover)
  nnoremap <silent> <C-]> :LspDefinition<CR>

  let g:lsp_settings = {
        \ 'gopls': {
        \  'workspace_config': {
        \    'usePlaceholders': v:true,
        \    'analyses': {
        \      'fillstruct': v:true,
        \      'test': v:true,
        \    },
        \  },
        \  'initialization_options': {
        \    'usePlaceholders': v:true,
        \    'analyses': {
        \      'fillstruct': v:true,
        \    },
        \  },
        \ },
        \ 'eslint-language-server': {
        \   'allowlist': ['javascript', 'typescript', 'vue'],
        \ },
        \ 'efm-langserver': {
        \   'disabled': 0,
        \   'allowlist': ['markdown'],
        \  }
        \}

  let g:lsp_settings_filetype_typescript = ['typescript-language-server', 'eslint-language-server', 'deno']

  function! s:on_lsp_buffer_enabled() abort
    setlocal completeopt=menu
    setlocal omnifunc=lsp#complete
  endfunction

  function! s:autofmt() abort
    if &ft == 'typescript'
      LspDocumentFormatSync
    endif
  endfunction

  augroup lsp_autofmt
    au!
    au BufWrite * call s:autofmt()
  augroup END

  augroup lsp_install
    au!
    au User lsp_buffer_enabled call s:on_lsp_buffer_enabled()
  augroup END

  "augroup vim_lsp_golangci_lint_langserver
  "  au!
  "  autocmd User lsp_setup call lsp#register_server({
  "      \ 'name': 'golangci-lint-langserver',
  "      \ 'cmd': {server_info->['golangci-lint-langserver']},
  "      \ 'initialization_options': {'command': ['golangci-lint', 'run', 'json']},
  "      \ 'allowlist': ['go'],
  "      \ })
  "augroup END
endif
" }}}

" vim-markdown {{{
let g:vim_markdown_folding_disabled = 1
" }}}

" emmet {{{
let g:user_emmet_install_global = 0
let g:user_emmet_settings = {
      \   'variables': {
      \     'lang' : 'ja'
      \   }
      \ }
let g:user_emmet_leader_key = '<C-g>'

augroup emmet
  au!
  au FileType vue,html,css EmmetInstall
  au FileType vue,html,css imap <buffer> <C-f> <plug>(emmet-expand-abbr)
augroup END
" }}}

" sonictemplate.vim {{{
let g:sonictemplate_vim_template_dir = ["~/.vim/template"]
let g:sonictemplate_author = 'skanehira'
let g:sonictemplate_license = 'MIT'
let g:sonictemplate_vim_template_dir = expand('~/.vim/sonictemplate')
imap <silent> <C-l> <plug>(sonictemplate-postfix)
" }}}

" vimhelpgenarator {{{
let g:vimhelpgenerator_version = ''
let g:vimhelpgenerator_author = 'Author: skanehira <sho19921005@gmail.com>'
let g:vimhelpgenerator_uri = 'https://github.com/skanehira/'
let g:vimhelpgenerator_defaultlanguage = 'en'
" }}}

" {{{ MemoList
let g:memolist_fzf = 1
" }}}

" {{{ gh.vim
let g:gh_open_issue_on_create = 1
function! s:gh_map_apply() abort
  if !exists('g:loaded_gh')
    return
  endif
  call gh#map#add('gh-buffer-issue-list', 'nmap', 'e', '<Plug>(gh_issue_edit)')
  call gh#map#add('gh-buffer-issue-list', 'nmap', 'gm', '<Plug>(gh_issue_open_comment)')
  call gh#map#add('gh-buffer-issue-list', 'nmap', 'y', '<Plug>(gh_issue_url_yank)')
  call gh#map#add('gh-buffer-issue-comment-list', 'nmap', 'n', '<Plug>(gh_issue_comment_new)')
  call gh#map#add('gh-buffer-issue-comment-list', 'nmap', 'e', '<Plug>(gh_issue_comment_edit)')
  call gh#map#add('gh-buffer-issue-edit', 'nmap', 'gm', '<Plug>(gh_issue_comment_open_on_issue)')
  call gh#map#add('gh-buffer-pull-list', 'nmap', 'y', '<Plug>(gh_pull_url_yank)')
  call gh#map#add('gh-buffer-project-list', 'nmap', 'y', '<Plug>(gh_project_url_yank)')
  call gh#map#add('gh-buffer-project-column-list', 'nmap', 'y', '<Plug>(gh_projects_card_url_yank)')
  call gh#map#add('gh-buffer-project-column-list', 'nmap', 'o', '<Plug>(gh_projects_card_open)')
  call gh#map#add('gh-buffer-action-list', 'nmap', 'o', '<Plug>(gh_actions_open_browser)')
  call gh#map#add('gh-buffer-action-list', 'nmap', 'y', '<Plug>(gh_actions_yank_url)')
  call gh#map#add('gh-buffer-gist-list', 'nmap', 'e', '<Plug>(gh_gist_edit_file)')
  call gh#map#add('gh-buffer-gist-list', 'nmap', 'y', '<Plug>(gh_gist_list_yank)')
  call gh#map#add('gh-buffer-bookmark-list', 'nmap', '<CR>', ':e <C-r>=getline(".")<CR><CR>')
endfunction

augroup gh-maps
  au!
  au VimEnter * call <SID>gh_map_apply()
augroup END

nnoremap <silent> gh :new gh://bookmarks<CR>
" }}}

" {{{ vim-operator-replace
vmap p <Plug>(operator-replace)
" }}}

" {{{ getpr.vim
map go <Plug>(getpr-open)
map gy <Plug>(getpr-yank)
" }}}

" {{{ gyazo.vim
let g:gyazo_insert_markdown_url = 1
nmap gup <Plug>(gyazo-upload)
" }}}

" {{{ vim-choosewin
nmap <C-f> <Plug>(choosewin)
" }}}

" {{{ lightline
let g:lightline = {
      \ 'colorscheme': 'wombat',
      \ 'active': {
      \ 'left': [ ['mode', 'paste'], ['readonly', 'branchName', 'filepath', 'modified'] ]
      \ },
      \ 'component_function':{
      \ 'filepath': 'FilePath',
      \ },
      \ }

function! FilePath()
  if winwidth(0) > 90
    return expand("%:s")
  else
    return expand("%:t")
  endif
endfunction
" }}}

" {{{ command.vim
nmap c: <Plug>(command_buffer_open)
" }}}

syntax enable " シンタックスを有効にする
filetype plugin indent on " ファイル形式別プラグインとインデントを有効にする

" カラースキームを使う {{{
colorscheme iceberg
" icebergはターミナルを16色に設定するのを回避
if !has('nvim')
  augroup iceberg
    au!
    au ColorScheme * unlet g:terminal_ansi_colors
  augroup END
endif
set background=dark
" visual modeのhighlight変更
hi Visual ctermfg=159 ctermbg=23 guifg=#b3c3cc guibg=#384851
" }}}

" オプション {{{
set encoding=utf-8
set t_ut=
set fileencodings=utf-8,iso-2022-jp,euc-jp,sjis
set fileformats=unix,dos,mac
set belloff=all " ミュート
set backspace=2 " バックスペースとCtrl+hで削除を有効にする
set smartindent autoindent " 改行時自動インデント
set incsearch " インクリメントサーチを有効にする
set ignorecase " 検索時大文字小文字を区別しない
set smartcase " 検索時に大文字を入力した場合ignorecaseが無効になる
set hlsearch " ハイライトサーチを有効にする
set undolevels=1000 " undoできる最大数
set scrolloff=100 " カーソルが常に中央に来るようにする
set lazyredraw ttyfast " マクロで効果発揮
set synmaxcol=256 "一行が長いファイルをsyntaxを制御することで軽くする
set cursorline " カーソルラインを表示する
set wildmenu " wildmenuを有効にする
set virtualedit=block " 矩形選択時に文字の無いところまで選択範囲を広げる
set helplang=ja " ヘルプの言語を日本語優先にする
set autowrite " 他のバッファに移動する時に自動保存
set noswapfile " swapファイルを作成しない
set showtabline=2 " タブを表示しない
set noshowmode
set laststatus=2
"set noequalalways " ウィンドウが同じサイズにならないようにする
"set winfixheight " ウィンドウの高さを保つ
"set winfixwidth " ウィンドウの幅を保つ
set list listchars=tab:»-,trail:-,extends:»,precedes:«,nbsp:% " listの設定
if !has('nvim')
  set nrformats+=unsigned " 数値の加減算を考慮
  set cursorlineopt=line " 行のみハイライト
  set sessionoptions=blank,buffers,curdir,folds,help,tabpages,winsize,terminal " セッションで保存する対象
endif
if has("mac") | set clipboard+=unnamed | else | set clipboard^=unnamedplus | endif " クリップボードを共有
set diffopt=vertical

" 拡張子ごとのインデント設定 {{{
set tabstop=2 shiftwidth=2 softtabstop=2

augroup fileTypeIndent
  au!
  au FileType go setlocal tabstop=4 shiftwidth=4
  au FileType vim setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType php setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType html setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType javascript setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType typescript setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType vue  setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType yaml setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType json setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType sh setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType fish setlocal tabstop=2 softtabstop=2 shiftwidth=2 expandtab
  au FileType zsh setlocal tabstop=2 shiftwidth=2 expandtab
  au FileType rust setlocal tabstop=4 softtabstop=4 shiftwidth=4 expandtab
  au FileType markdown setlocal tabstop=2 shiftwidth=2 expandtab
augroup END
" }}}

" {{{ シンタックスをクリア
augroup fileTypeSyntaxClear
  au!
  au FileType go syntax clear
  au FileType vim syntax clear
  au FileType html syntax clear
  au FileType javascript syntax clear
  au FileType typescript syntax clear
  au FileType vue  syntax clear
augroup END
" }}}

" grepした結果をquickfixに表示する {{{
augroup grepwindow
  au!
  au QuickFixCmdPost *grep* cwindow
augroup END
" }}}

" カーソルラインの位置を保存する {{{
augroup cursorlineRestore
  au!
  au BufReadPost *
        \ if line("'\"") > 0 && line("'\"") <= line("$") |
        \   exe "normal! g'\"" |
        \ endif
augroup END
" }}}

" undoの保存先 {{{
if has('persistent_undo')
  let undo_path = expand('~/.vim/undo')
  " ディレクトリが存在しない場合は作成
  if !isdirectory(undo_path)
    call mkdir(undo_path, 'p')
  endif
  exe 'set undodir=' .. undo_path
  set undofile
endif
" }}}
" }}}

" キーマップ {{{

" ^と0を0に統一
nnoremap <expr> 0 getline('.')[0 : col('.') - 2] =~# '^\s\+$' ? '0' : '^'

" *でカーソルを移動しないようにする
noremap * *N

" ファイル保存と終了
nnoremap <Leader>w :w<CR>

" 検索
nnoremap <C-G><C-G> :Grep <C-R><C-W><CR>

" 置換
nnoremap <Leader>re :%s;\<<C-R><C-W>\>;g<Left><Left>;

" ハイライトを削除する
nnoremap <silent> <Esc><Esc> :nohlsearch<CR>

" vimrcを開く
nnoremap <Leader>. :new ~/.vimrc<CR>
nnoremap <Leader>s :exe "source" expand("%")<CR>

" テキストオブジェクトキーマッピング {{{
onoremap 8 i(
onoremap 2 i"
onoremap 7 i'
onoremap @ i`
onoremap [ i[
onoremap { i{

onoremap a8 a(
onoremap a2 a"
onoremap a7 a'
onoremap a@ a`
onoremap a[ a[
onoremap a{ a{

" visual
nnoremap v8 vi(
nnoremap v2 vi"
nnoremap v7 vi'
nnoremap v@ vi`
nnoremap v[ vi[
nnoremap v{ vi{

nnoremap va8 va(
nnoremap va2 va"
nnoremap va7 va'
nnoremap va@ va`
nnoremap va[ va[
nnoremap va{ va{
" }}}

" 行先頭と行末
noremap H ^
noremap L g_

" タブ切り替え
nnoremap <C-l> gt
nnoremap <C-h> gT

" ターミナル関連 {{{
tnoremap <C-]> <C-\><C-n>
if has('nvim')
  nnoremap <Leader>tm :new term<CR>
  function! s:term_start(cmd) abort
    call termopen(a:cmd)
  endfunction
else
  nnoremap <Leader>tm :term<CR>
  function! s:term_start(cmd) abort
    let old = bufnr()
    exe printf('term ++curwin ++shell %s', a:cmd)
    exe "bw!" old
    nnoremap <buffer> <silent> <CR> :bw!<CR>
  endfunction
endif
nnoremap <Leader>ttm :tabnew term<CR>

function! s:termopen() abort
  let bufname = bufname()
  let parts = split(bufname, ' ')
  let cmd = &shell
  if len(parts) > 1
    " like :new term [command] [args]
    let cmd = join(parts[1:], " ")
  endif
  call s:term_start(cmd)
endfunction

if has('nvim')
  augroup neovim-terminal
    au!
    au TermOpen * startinsert
  augroup END
else
  set termwinkey=<C-]>
  " ターミナルノーマルモード
endif

augroup terminal
  au!
  au BufNewFile term* ++nested call s:termopen()
augroup END
" }}}

" 改行
nnoremap <C-j> o<ESC>
nnoremap <C-k> O<ESC>
nnoremap o A<CR>

" Emacs風のキーマップ
imap <C-h> <BS>
inoremap <C-k> <C-o>C
inoremap <silent> <C-f> <Right>
inoremap <silent> <C-b> <Left>
inoremap <silent> <C-e> <C-o>A
inoremap <silent> <C-a> <C-o>I

" C-v でペースト
inoremap <expr> <C-v> printf('<C-r><C-o>%s', has('linux') \|\| has('unix') ? '+' : '*')
cnoremap <expr> <C-v> printf('<C-r><C-o>%s', has('linux') \|\| has('unix') ? '+' : '*')

" ヘルプ
augroup help-mapping
  au!
  au FileType help nnoremap <buffer> <silent>q :bw!<CR>
augroup END
" }}}

" コマンドラインで単語移動 {{{
cnoremap <C-b> <Left>
cnoremap <C-f> <Right>
cnoremap <C-a> <Home>
" }}}

" Better <C-n>/<C-p> in Command {{{
cnoremap <C-p> <Up>
cnoremap <C-n> <Down>
cnoremap <Up> <C-p>
cnoremap <Down> <C-n>
" }}}

" {{{ リポジトリに移動
function! s:cd_repo(shell, repo) abort
  exe 'lcd' trim(system('ghq root')) .. '/' .. a:repo
  pwd
endfunction

function! s:repo(cb) abort
  if executable('ghq') && exists('*fzf#run()') && executable('fzf')
    call fzf#run({
          \ 'source': systemlist('ghq list'),
          \ 'sink': a:cb,
          \ 'down': '40%'},
          \ )
  else
    echo "doesn't installed ghq or fzf.vim(require fzf)"
  endif
endfunction

command! Repo call s:repo(function('s:cd_repo', [&shell]))
" }}}

" {{{ エラーメッセージ出力
function! s:echo_err(message) abort
  echohl ErrorMsg
  redraw
  echo a:message
  echohl None
endfunction
" }}}

" {{{ ディレクトリ自動生成
function! s:auto_mkdir(dir)
  if !isdirectory(a:dir)
    call mkdir(iconv(a:dir, &encoding, &termencoding), 'p')
  endif
endfunction
augroup auto-mkdir
  au!
  au BufWritePre * call s:auto_mkdir(expand('<afile>:p:h'))
augroup END
" }}}

" {{{ lazygit
nnoremap <silent> gp :call system('source ~/.zshrc && tmuxpopup lazygit')<CR>
" }}}

" {{{ golangci-lint
function! s:golangci(...) abort
  if !executable('golangci-lint')
    call s:echo_err('golangci-lint does not installed')
    return
  endif

  if a:0 > 0
    let opts = a:000
  else
    let opts = get(g:, 'golangci_opts', ['misspell', 'unparam', 'stylecheck',
          \ 'gosec', 'prealloc', 'gocritic', 'gomnd', 'unconvert'])
  endif

  if get(g:, 'golangci_disable_all', 0)
    let cmd = printf('golangci-lint run --disable-all --print-issued-lines=false --enable=%s', join(opts, ','))
  else
    let cmd = printf('golangci-lint run --print-issued-lines=false --enable=%s', join(opts, ','))
  endif
  cexpr system(cmd) | cw
endfunction
command! -nargs=* GolangCI call <SID>golangci(<f-args>)
" }}}

" {{{ grep
function! s:grep(word) abort
  let cmd = printf('ag %s', a:word)
  lgete system(cmd) | lw
endfunction

command! -nargs=1 -complete=file Grep call <SID>grep(<q-args>)
" }}}

" {{{ zenn
function! s:create_zenn_article(article_name) abort
  let date = strftime("%Y-%m-%d")
  let slug = date . "-" . a:article_name
  call system("npx zenn new:article --emoji 🦍 --slug " . slug )
  let article_path = "articles/" . slug . ".md"
  exe "edit " . article_path
endfunction

command! -nargs=1 ZennCreateArticle call <SID>create_zenn_article(<f-args>)
" }}}

" {{{ goimports
" augroup goimports
"   function! s:goimports() abort
"     let pos = getcurpos()
"     %!goimports
"     call setpos('.', pos)
"   endfunction
"   au!
"   au! FileType go nnoremap <silent> <Leader>i :call <SID>goimports()<CR>
" augroup END
" }}}

" {{{ vim-test
let test#javascript#denotest#options = {
    \ 'all': '--unstable -A'
    \ }
nnoremap <silent> <space>tn <cmd>TestNearest<CR>
" }}}

" {{{ open-browser.vim
nmap gop <Plug>(openbrowser-open)
" }}}

" {{{ qfopen.vim
augroup qfopen-settings
  function! s:qfopen_keymap() abort
    nmap <buffer> a <Plug>(qfopen-action)
    nmap <buffer> <C-v> <Plug>(qfopen-open-vsplit)
    nmap <buffer> <C-x> <Plug>(qfopen-open-split)
    nmap <buffer> <C-t> <Plug>(qfopen-open-tab)
    nnoremap <buffer> <silent> q :q<CR>
  endfunction
  au!
  au FileType qf call s:qfopen_keymap()
augroup END
" }}}

" {{{ for dlv
function! Get_breakpoint_line() abort
  let line = printf("break %s:%s", expand("%:p"), line("."))
  call setreg("*", line)
  echom line
endfunction

nnoremap gdy :call Get_breakpoint_line()<CR>
" }}}

" {{{ denops-delve.vim
map gdb <Plug>(dlv-breakpoint)
map gdr <Plug>(dlv-breakpoint-clear)
map gds <Plug>(dlv-breakpoints)
map gdc <Plug>(dlv-continue)
map gdn <Plug>(dlv-next)
map gdi <Plug>(dlv-step)
map gdo <Plug>(dlv-stepout)
map gdp <Plug>(dlv-print)
" }}}

function! ActiveBrowserTab(line) abort
  let id = a:line[1:match(a:line, "\]", 0, 1)-1]
  call system("open -b $CHROME_BUNDLE_IDENTIFIER")
  call system("chrome-cli activate -t " .. id)
endfunction

nnoremap <silent> gdt :call fzf#run({
      \ "source": systemlist("chrome-cli list tabs"),
      \ "window": 'botright 10new',
      \ "sink": function("ActiveBrowserTab")})<CR>

" {{{ markdown
let s:clipboard_register = has('linux') || has('unix') ? '+' : '*'
function! InsertMarkdownLink() abort
  " use register `9`
  let old = getreg('9')
  let link = trim(getreg(s:clipboard_register))
  if link !~# '^http.*'
    normal! gvp
    return
  endif

  " replace `[text](link)` to selected text
  normal! gv"9y
  let word = getreg(9)
  let newtext = printf('[%s](%s)', word, link)
  call setreg(9, newtext)
  normal! gv"9p

  " restore old data
  call setreg(9, old)
endfunction

augroup markdown-insert-link
  au!
  au FileType markdown vnoremap <buffer> <silent> p :call InsertMarkdownLink()<CR>
augroup END
" }}}

" {{{ ddc.vim
" Use matcher_head and sorter_rank.
" https://github.com/Shougo/ddc-matcher_head
" https://github.com/Shougo/ddc-sorter_rank
call ddc#custom#patch_global('sourceOptions', {
      \ '_': {
      \   'matchers': ['matcher_head'],
      \   'sorters': ['sorter_rank']},
      \ })

" add sources
" call ddc#custom#patch_global('sources', ['around'])
call ddc#custom#patch_global('sources', ['gh_issues'])
call ddc#custom#patch_global('sourceOptions', {
      \ 'gh_issues': {'matcherKey': 'menu'},
      \ })

" Change source options
inoremap <silent><expr> <TAB>
\ pumvisible() ? '<C-n>' :
\ (col('.') <= 1 <Bar><Bar> getline('.')[col('.') - 2] =~# '\s') ?
\ '<TAB>' : ddc#map#manual_complete()

" <S-TAB>: completion back.
inoremap <expr><S-TAB>  pumvisible() ? '<C-p>' : '<C-h>'
call ddc#enable()
" }}}

" vim: foldmethod=marker
